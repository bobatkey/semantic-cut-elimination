\begin{code}[hide]
  open import Level using (zero)
  import Relation.Binary.Construct.Closure.Equivalence as EqClosure
  open EqClosure using (EqClosure)
  import Relation.Binary.Construct.Closure.ReflexiveTransitive as Star
  open Star using (Star)
\end{code}
\begin{code}[hide]
  module mav-syntax (Atom : Set) where
\end{code}
\section{Multiplicative-Additive System Virtual}\label{sec:mav-syntax}

TODO: introduce MAV and Deep Inference.

Formulas:
\begin{code}[hide]
  infix 20 _⁺
  infix 20 _⁻
  infix 10 _⅋_
  infix 10 _⊗_
  infix 10 _&_
  infix 10 _⊕_
  infix 10 _▷_
\end{code}

\begin{code}
  data Formula : Set where
    I    : Formula
    _⁺   : Atom → Formula
    _⁻   : Atom → Formula
    _⅋_  : Formula → Formula → Formula
    _⊗_  : Formula → Formula → Formula
    _&_  : Formula → Formula → Formula
    _⊕_  : Formula → Formula → Formula
    _▷_  : Formula → Formula → Formula
\end{code}

\begin{displaymath}
  P, Q
  \Coloneq A^{+}
  \mid     A^{-}
  \mid     I
  \mid     P \bvtens Q
  \mid     P \bvparr Q
  \mid     P \bvwith Q
  \mid     P \bvplus Q
  \mid     P \bvseq  Q
\end{displaymath}

\begin{code}[hide]
  variable
    A A′ A₁ A₂ : Atom
    P P′ P₁ P₂ : Formula
    Q Q′ Q₁ Q₂ : Formula
    R R′ R₁ R₂ : Formula
    S S′ S₁ S₂ : Formula
\end{code}

Negation:
% \begin{displaymath}
%   \begin{array}{lcl}
%     (a)^\perp           & = & a^\perp                     \\
%     (a^\perp)^\perp     & = & a                           \\
%     (I)^\perp           & = & I                           \\
%     (P \otimes Q)^\perp & = & (P)^\perp \parr   (Q)^\perp \\
%     (P \parr   Q)^\perp & = & (P)^\perp \otimes (Q)^\perp \\
%     (P \with   Q)^\perp & = & (P)^\perp \oplus  (Q)^\perp \\
%     (P \oplus  Q)^\perp & = & (P)^\perp \with   (Q)^\perp \\
%     (P ; Q)^\perp       & = & (P)^\perp ; (Q)^\perp
%   \end{array}
% \end{displaymath}

\begin{code}[hide]
  infix 15 ¬_
\end{code}

\begin{code}
  ¬_ : Formula → Formula
  ¬ I          = I
  ¬ A ⁺        = A ⁻
  ¬ A ⁻        = A ⁺
  ¬ (P  ⅋  Q)  = ¬ P  ⊗  ¬ Q
  ¬ (P  ⊗  Q)  = ¬ P  ⅋  ¬ Q
  ¬ (P  &  Q)  = ¬ P  ⊕  ¬ Q
  ¬ (P  ⊕  Q)  = ¬ P  &  ¬ Q
  ¬ (P  ▷  Q)  = ¬ P  ▷  ¬ Q
\end{code}

\begin{code}[hide]
  infix 5 _∼_
\end{code}

\begin{code}
  data _∼_ : Formula → Formula → Set where
    ⊗-assoc    : P  ⊗ (Q  ⊗ R)  ∼ (P ⊗ Q) ⊗ R
    ⊗-comm     : P  ⊗ Q         ∼ Q ⊗ P
    ⊗-unit     : P  ⊗ I         ∼ P

    ⅋-assoc    : P  ⅋ (Q  ⅋ R)  ∼ (P ⅋ Q) ⅋ R
    ⅋-comm     : P  ⅋ Q         ∼ Q ⅋ P
    ⅋-unit     : P  ⅋ I         ∼ P

    ▷-assoc    : P  ▷ (Q  ▷ R)  ∼ (P  ▷ Q) ▷ R
    ▷-runit    : P  ▷ I         ∼ P
    ▷-lunit    : I  ▷ P         ∼ P
\end{code}

\begin{code}[hide]
  infix 5 _≡_
\end{code}

\begin{code}
  _≡_ : Formula → Formula → Set
  _≡_ = EqClosure _∼_
\end{code}

\begin{code}[hide]
  infix 5 _⟶_
\end{code}

\begin{code}
  data _⟶_ : Formula → Formula → Set where
    axiom      : P ⅋ ¬ P  ⟶ I
    cut        : I        ⟶ P ⊗ ¬ P

    tidy       : I & I              ⟶ I
    switch     : (P ⊗ Q) ⅋ R        ⟶ P ⊗ (Q ⅋ R)
    sequence   : (P ▷ Q) ⅋ (R ▷ S)  ⟶ (P ⅋ R) ▷ (Q ⅋ S)
    left       : P ⊕ Q              ⟶ P
    right      : P ⊕ Q              ⟶ Q
    external   : (P & Q) ⅋ R        ⟶ (P ⅋ R) & (Q ⅋ R)
    medial     : (P ▷ Q) & (R ▷ S)  ⟶ (P & R) ▷ (Q & S)

    equiv      : P  ≡ Q   →  P      ⟶  Q
    _⟨⅋        : P  ⟶  R  →  P ⅋ Q  ⟶  R ⅋ Q
    ⅋⟩_        : Q  ⟶  S  →  P ⅋ Q  ⟶  P ⅋ S
    _⟨▷        : P  ⟶  R  →  P ▷ Q  ⟶  R ▷ Q
    ▷⟩_        : Q  ⟶  S  →  P ▷ Q  ⟶  P ▷ S
    _⟨⊗        : P  ⟶  R  →  P ⊗ Q  ⟶  R ⊗ Q
    ⊗⟩_        : Q  ⟶  S  →  P ⊗ Q  ⟶  P ⊗ S
    _⟨&        : P  ⟶  R  →  P & Q  ⟶  R & Q
    &⟩_        : Q  ⟶  S  →  P & Q  ⟶  P & S
\end{code}

\begin{code}[hide]
  infix 5 _⟶⋆_
\end{code}

\begin{code}
  _⟶⋆_ : Formula → Formula → Set
  _⟶⋆_ = Star _⟶_
\end{code}
