{-# OPTIONS --postfix-projections --safe --without-K #-}

open import Level using (suc)
open import NEL.Model
open import Function using (flip; id; _âˆ˜_; _on_)
open import Data.Product using (projâ‚; projâ‚‚)
open import Data.Sum using (_âŠ_; [_,_])
open import Relation.Binary
open import Relation.Binary.Construct.Union using (_âˆª_)
import Relation.Binary.Construct.Union as Union
open import Relation.Binary.Construct.Closure.ReflexiveTransitive using (Star; Îµ; _â—…_; _â–»_)
import Relation.Binary.Construct.Closure.ReflexiveTransitive as Star
open import Relation.Binary.Construct.Closure.Equivalence using (EqClosure)
import Relation.Binary.Construct.Closure.Equivalence as EqClosure
open import Relation.Binary.Construct.Closure.Symmetric using (SymClosure; fwd; bwd)
import Relation.Binary.Construct.Closure.Symmetric as SymClosure
import Relation.Binary.Construct.Flip.EqAndOrd as Flip

module NEL.Interpretation
    {a c â„“â‚ â„“â‚‚}
    (Atom : Set a)
    (M : Model c â„“â‚ â„“â‚‚)
    (V : Atom â†’ M .Model.Carrier)
  where

open import NEL.Structure Atom
open import NEL.Symmetric Atom

private
  variable
    P Pâ€² : Structure
    Q Qâ€² : Structure

open Model M

âŸ¦_âŸ§ : Structure â†’ Carrier
âŸ¦ `I     âŸ§ = I
âŸ¦ `+ x   âŸ§ = V x
âŸ¦ `- x   âŸ§ = Â¬ (V x)
âŸ¦ `! P   âŸ§ = ï¼ âŸ¦ P âŸ§
âŸ¦ `? P   âŸ§ = ï¼Ÿ âŸ¦ P âŸ§
âŸ¦ P `â— Q âŸ§ = âŸ¦ P âŸ§ â— âŸ¦ Q âŸ§
âŸ¦ P `â…‹ Q âŸ§ = âŸ¦ P âŸ§ â…‹ âŸ¦ Q âŸ§
âŸ¦ P `âŠ— Q âŸ§ = âŸ¦ P âŸ§ âŠ— âŸ¦ Q âŸ§

dual-ok : âˆ€ P â†’ âŸ¦ `Â¬ P âŸ§ â‰ˆ Â¬ âŸ¦ P âŸ§
dual-ok `I = mix
dual-ok (`+ x) = Eq.refl
dual-ok (`- x) = Eq.sym (Â¬-involutive _)
dual-ok (`! P) = Eq.trans (ï¼Ÿ-cong (dual-ok P)) (Â¬-cong (ï¼-cong (Â¬-involutive _)))
dual-ok (`? P) = Eq.trans (ï¼-cong (dual-ok P)) (Eq.sym (Â¬-involutive _))
dual-ok (P `â— Q) = Eq.trans (â—-cong (dual-ok P) (dual-ok Q)) (Eq.sym â—-self-dual)
dual-ok (P `â…‹ Q) = Eq.trans (âŠ—-cong (dual-ok P) (dual-ok Q)) (Eq.sym (Â¬-involutive _))
dual-ok (P `âŠ— Q) =
  Eq.trans (â…‹-cong (dual-ok P) (dual-ok Q)) (Â¬-cong (âŠ—-cong (Â¬-involutive _) (Â¬-involutive _)))

-- Interpret the equivalence axioms
âŸ¦_âŸ§eq-ax : P âˆ¼ Q â†’ âŸ¦ P âŸ§ â‰ˆ âŸ¦ Q âŸ§
âŸ¦ `â—-assoc     âŸ§eq-ax = â—-assoc _ _ _
âŸ¦ `â—-identityÊ³ âŸ§eq-ax = Eq.trans (â—-cong Eq.refl I-eq-J) (â—-identityÊ³ _)
âŸ¦ `â—-identityË¡ âŸ§eq-ax = Eq.trans (â—-cong I-eq-J Eq.refl) (â—-identityË¡ _)
âŸ¦ `âŠ—-assoc     âŸ§eq-ax = âŠ—-assoc _ _ _
âŸ¦ `âŠ—-comm      âŸ§eq-ax = âŠ—-comm _ _
âŸ¦ `âŠ—-identityÊ³ âŸ§eq-ax = âŠ—-identityÊ³ _
âŸ¦ `â…‹-assoc     âŸ§eq-ax = â…‹-assoc _ _ _
âŸ¦ `â…‹-comm      âŸ§eq-ax = â…‹-comm _ _
âŸ¦ `â…‹-identityÊ³ âŸ§eq-ax = Eq.trans (â…‹-cong Eq.refl mix) (â…‹-identityÊ³ _)

-- The interpretation is closed under congruence
module _ {â„“} {_ğ“¡_ : Rel Structure â„“} where

  cong : (f : âˆ€ {P Q} â†’ P ğ“¡ Q â†’ âŸ¦ P âŸ§ â‰ˆ âŸ¦ Q âŸ§) â†’ CongClosure _ğ“¡_ P Q â†’ âŸ¦ P âŸ§ â‰ˆ âŸ¦ Q âŸ§
  cong f (emb Ï†)   = f Ï†
  cong f (Ï† `âŸ¨â—) = â—-cong (cong f Ï†) Eq.refl
  cong f (`â—âŸ© Ï†) = â—-cong Eq.refl (cong f Ï†)
  cong f (Ï† `âŸ¨âŠ—) = âŠ—-cong (cong f Ï†) Eq.refl
  cong f (`âŠ—âŸ© Ï†) = âŠ—-cong Eq.refl (cong f Ï†)
  cong f (Ï† `âŸ¨â…‹) = â…‹-cong (cong f Ï†) Eq.refl
  cong f (`â…‹âŸ© Ï†) = â…‹-cong Eq.refl (cong f Ï†)
  cong f (`!âŸ© Ï†) = ï¼-cong (cong f Ï†)

-- -- Interpret the equivalence
âŸ¦_âŸ§eq : P â‰ƒ Q â†’ âŸ¦ P âŸ§ â‰ˆ âŸ¦ Q âŸ§
âŸ¦_âŸ§eq = EqClosure.gfold isEquivalence âŸ¦_âŸ§ (cong âŸ¦_âŸ§eq-ax)

-- Interpret the reduction axioms
âŸ¦_âŸ§step-ax : P âŸ¶ Q â†’ âŸ¦ Q âŸ§ â‰² âŸ¦ P âŸ§
âŸ¦ `axiom P   âŸ§step-ax =
  trans coev (â…‹-mono refl (reflexive (Eq.sym (dual-ok P))))
âŸ¦ `cut P     âŸ§step-ax =
  trans (âŠ—-mono refl (reflexive (dual-ok P))) (trans ev (reflexive (Eq.sym mix)))
âŸ¦ `switch    âŸ§step-ax = linear-distrib
âŸ¦ `sequence  âŸ§step-ax = sequence
âŸ¦ `cosequence âŸ§step-ax = âŠ—-â—-entropy _ _ _ _

âŸ¦ `mon âŸ§step-ax = ï¼Ÿ-monoidal
âŸ¦ `eâ†‘ âŸ§step-ax = ï¼Ÿ-monoidal-unit
âŸ¦ `e âŸ§step-ax = ï¼-monoidal-unit
âŸ¦ `p âŸ§step-ax = pâ†“
âŸ¦ `dis âŸ§step-ax = ï¼Ÿ-discard
âŸ¦ `dup âŸ§step-ax = ï¼Ÿ-duplicate
âŸ¦ `Î´   âŸ§step-ax = ï¼Ÿ-dig
âŸ¦ `Îµ   âŸ§step-ax = ï¼Ÿ-derelict

-- The interpretation is closed under monotonicity
module _ {â„“} {_ğ“¡_ : Rel Structure â„“} where

  mono : (f : âˆ€ {P Q} â†’ P ğ“¡ Q â†’ âŸ¦ Q âŸ§ â‰² âŸ¦ P âŸ§) â†’ CongClosure _ğ“¡_ P Q â†’ âŸ¦ Q âŸ§ â‰² âŸ¦ P âŸ§
  mono f (emb Ï†)   = f Ï†
  mono f (Ï† `âŸ¨â—) = â—-mono (mono f Ï†) refl
  mono f (`â—âŸ© Ï†) = â—-mono refl (mono f Ï†)
  mono f (Ï† `âŸ¨âŠ—) = âŠ—-mono (mono f Ï†) refl
  mono f (`âŠ—âŸ© Ï†) = âŠ—-mono refl (mono f Ï†)
  mono f (Ï† `âŸ¨â…‹) = â…‹-mono (mono f Ï†) refl
  mono f (`â…‹âŸ© Ï†) = â…‹-mono refl (mono f Ï†)
  mono f (`!âŸ© Ï†) = ï¼-mono (mono f Ï†)

-- Interpret the reduction modulo the equivalence
âŸ¦_âŸ§step : P âŸ¶â‚Œ Q â†’ âŸ¦ Q âŸ§ â‰² âŸ¦ P âŸ§
âŸ¦_âŸ§step = [ reflexive âˆ˜ Eq.sym âˆ˜ âŸ¦_âŸ§eq , mono âŸ¦_âŸ§step-ax ]

-- Interpret the reflexive-transitive closure of reduction
âŸ¦_âŸ§steps : P âŸ¶â‹† Q â†’ âŸ¦ Q âŸ§ â‰² âŸ¦ P âŸ§
âŸ¦_âŸ§steps {P} {Q} = Star.gfold âŸ¦_âŸ§ (flip _â‰²_) (Î» Ï† Ïˆ â†’ trans Ïˆ âŸ¦ Ï† âŸ§step) {P} {Q} {Q} refl
