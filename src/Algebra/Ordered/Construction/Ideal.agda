{-# OPTIONS --postfix-projections --safe --without-K --no-exact-split #-}

open import Level
open import Algebra
open import Algebra.Definitions
open import Algebra.Ordered
open import Algebra.Ordered.Definitions
open import Algebra.Ordered.Consequences
open import Algebra.Ordered.Structures.Residuated
open import Algebra.Ordered.Structures.Duoidal
open import Function using (const; flip)
open import Data.Product using (_Ã—_; _,_; -,_; projâ‚; projâ‚‚; Î£-syntax; âˆƒ; âˆƒ-syntax; <_,_>)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚; [_,_])
open import Data.Unit using (âŠ¤; tt)
open import Relation.Binary
open import Relation.Binary.Construct.Core.Symmetric as SymCore using (SymCore)
open import Relation.Binary.Lattice
open import Relation.Binary.PropositionalEquality as PropEq using (_â‰¡_)
import Relation.Binary.Construct.Flip.EqAndOrd as Flip
open import Relation.Unary using (Pred; _âŠ†_)
import Relation.Binary.Reasoning.PartialOrder as PosetReasoning
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

module Algebra.Ordered.Construction.Ideal {c â„“â‚ â„“â‚‚} (pomagma : Pomagma c â„“â‚ â„“â‚‚) where

open Pomagma pomagma
  using
    ( Carrier
    ; _â‰ˆ_
    ; _â‰¤_
    ; poset
    )
  renaming
    ( _âˆ™_        to _+_
    ; mono       to +-mono
    ; monoË¡      to +-monoË¡
    ; monoÊ³      to +-monoÊ³
    ; refl       to â‰¤-refl
    ; trans      to â‰¤-trans
    )

open import Algebra.Ordered.Construction.LowerSet poset as P
  using
    ( PreSheaf
    ; ICarrier
    ; â‰¤-closed
    ; _â‰¤áµ–_
    ; *â‰¤áµ–*
    ; â‰¤áµ–-refl
    ; â‰¤áµ–-trans
    ; _â‰ˆáµ–_
    ; Î·áµ–
    ; Î·áµ–-mono
    ; _âˆ¨áµ–_
    ; injâ‚áµ–
    ; injâ‚‚áµ–
    ; [_,_]áµ–
    )

private
  variable
    w x y z : Carrier
    â„“x â„“y â„“z : Level
    X : Pred Carrier â„“x
    Y : Pred Carrier â„“y
    Z : Pred Carrier â„“z
    F Fâ‚ Fâ‚‚ : PreSheaf
    G Gâ‚ Gâ‚‚ : PreSheaf
    H Hâ‚ Hâ‚‚ : PreSheaf

record Ideal : Set (suc (c âŠ” â„“â‚‚)) where
  no-eta-equality
  field
    ICarrier : Carrier â†’ Set (c âŠ” â„“â‚‚)
    â‰¤-closed : x â‰¤ y â†’ ICarrier y â†’ ICarrier x
    +-closed : ICarrier x â†’ ICarrier y â†’ ICarrier (x + y)
open Ideal

private
  variable
    ğ“• ğ“•â‚ ğ“•â‚‚ : Ideal
    ğ“– ğ“–â‚ ğ“–â‚‚ : Ideal
    ğ“— ğ“—â‚ ğ“—â‚‚ : Ideal

infix 4 _â‰¤â±_

record _â‰¤â±_ (ğ“• ğ“– : Ideal) : Set (c âŠ” â„“â‚‚) where
  no-eta-equality
  field
    *â‰¤â±* : ğ“• .ICarrier âŠ† ğ“– .ICarrier
open _â‰¤â±_

infix 4 _â‰ˆâ±_

_â‰ˆâ±_ : Ideal â†’ Ideal â†’ Set (c âŠ” â„“â‚‚)
_â‰ˆâ±_ = SymCore _â‰¤â±_

â‰¤â±-refl : ğ“• â‰¤â± ğ“•
â‰¤â±-refl .*â‰¤â±* ğ“•x = ğ“•x

â‰¤â±-trans : ğ“• â‰¤â± ğ“– â†’ ğ“– â‰¤â± ğ“— â†’ ğ“• â‰¤â± ğ“—
â‰¤â±-trans ğ“•â‰¤ğ“– ğ“–â‰¤ğ“— .*â‰¤â±* z = ğ“–â‰¤ğ“— .*â‰¤â±* (ğ“•â‰¤ğ“– .*â‰¤â±* z)

-- FIXME: get rid of the propositional equality here
â‰¤â±-isPartialOrder : IsPartialOrder _â‰ˆâ±_ _â‰¤â±_
â‰¤â±-isPartialOrder = SymCore.isPreorderâ‡’isPartialOrder _â‰¤â±_ â‰¡-â‰¤â±-isPreorder
  where
    â‰¡-â‰¤â±-isPreorder : IsPreorder _â‰¡_ _â‰¤â±_
    â‰¡-â‰¤â±-isPreorder = record
      { isEquivalence = PropEq.isEquivalence
      ; reflexive = Î» { PropEq.refl â†’ â‰¤â±-refl }
      ; trans = â‰¤â±-trans
      }

open IsPartialOrder â‰¤â±-isPartialOrder
  using
    ( module Eq
    )
  renaming
    ( â‰¤-respË¡-â‰ˆ  to â‰¤â±-respË¡-â‰ˆâ±
    ; reflexive to â‰¤â±-reflexive
    ; isPreorder to â‰¤â±-isPreorder
    )
  public

â‰¤â±-poset : Poset _ _ _
â‰¤â±-poset = record
  { isPartialOrder = â‰¤â±-isPartialOrder
  }

â‰ˆâ±-setoid : Setoid _ _
â‰ˆâ±-setoid = record
  { isEquivalence = Poset.isEquivalence â‰¤â±-poset
  }

------------------------------------------------------------------------------
-- From ideals to lower sets
U : Ideal â†’ PreSheaf
U ğ“• .ICarrier = ğ“• .ICarrier
U ğ“• .â‰¤-closed = ğ“• .â‰¤-closed

U-mono : ğ“• â‰¤â± ğ“– â†’ U ğ“• â‰¤áµ– U ğ“–
U-mono ğ“•â‰¤ğ“– .*â‰¤áµ–* = ğ“•â‰¤ğ“– .*â‰¤â±*

U-cong : ğ“• â‰ˆâ± ğ“– â†’ U ğ“• â‰ˆáµ– U ğ“–
U-cong (ğ“–â‰¤ğ“• , ğ“•â‰¤ğ“–) = U-mono ğ“–â‰¤ğ“• , U-mono ğ“•â‰¤ğ“–

------------------------------------------------------------------------------
-- Turn a lower set into an ideal by closing under +

data ctxt (F : PreSheaf) : Set (c âŠ” â„“â‚‚) where
  leaf : (x : Carrier) â†’ F .ICarrier x â†’ ctxt F
  node : ctxt F â†’ ctxt F â†’ ctxt F

sum : ctxt F â†’ Carrier
sum (leaf x _) = x
sum (node c d) = sum c + sum d

ctxt-map : F â‰¤áµ– G â†’ ctxt F â†’ ctxt G
ctxt-map Fâ‰¤G (leaf x Fx) = leaf x (Fâ‰¤G .*â‰¤áµ–* Fx)
ctxt-map Fâ‰¤G (node c d)  = node (ctxt-map Fâ‰¤G c) (ctxt-map Fâ‰¤G d)

ctxt-map-sum : (Fâ‰¤G : F â‰¤áµ– G) (c : ctxt F) â†’ sum c â‰¤ sum (ctxt-map Fâ‰¤G c)
ctxt-map-sum Fâ‰¤G (leaf x Fx) = â‰¤-refl
ctxt-map-sum Fâ‰¤G (node c d) = +-mono (ctxt-map-sum Fâ‰¤G c) (ctxt-map-sum Fâ‰¤G d)

Î± : PreSheaf â†’ Ideal
Î± F .ICarrier x = Î£[ t âˆˆ ctxt F ] (x â‰¤ sum t)
Î± F .â‰¤-closed xâ‰¤y (t , yâ‰¤t) = t , â‰¤-trans xâ‰¤y yâ‰¤t
Î± F .+-closed (s , xâ‰¤s) (t , yâ‰¤t) = node s t , +-mono xâ‰¤s yâ‰¤t

Î±-mono : F â‰¤áµ– G â†’ Î± F â‰¤â± Î± G
Î±-mono Fâ‰¤G .*â‰¤â±* (t , xâ‰¤t) = ctxt-map Fâ‰¤G t , â‰¤-trans xâ‰¤t (ctxt-map-sum Fâ‰¤G t)

Î±-cong : âˆ€ {F G} â†’ F â‰ˆáµ– G â†’ Î± F â‰ˆâ± Î± G
Î±-cong (Gâ‰¤F , Fâ‰¤G) = (Î±-mono Gâ‰¤F , Î±-mono Fâ‰¤G)

------------------------------------------------------------------------------
Î·â± : Carrier â†’ Ideal
Î·â± x = Î± (Î·áµ– x)

Î·â±-mono : x â‰¤ y â†’ Î·â± x â‰¤â± Î·â± y
Î·â±-mono xâ‰¤y = Î±-mono (Î·áµ–-mono xâ‰¤y)

------------------------------------------------------------------------------
-- U and Î± form a Galois connection

ideal-ctxt-closed : (t : ctxt (U ğ“•)) â†’ ğ“• .ICarrier (sum t)
ideal-ctxt-closed {ğ“•} (leaf x Ï•) = Ï•
ideal-ctxt-closed {ğ“•} (node c d) = ğ“• .+-closed (ideal-ctxt-closed c) (ideal-ctxt-closed d)

counit : Î± (U ğ“•) â‰¤â± ğ“•
counit {ğ“•} .*â‰¤â±* (t , xâ‰¤t) = ğ“• .â‰¤-closed xâ‰¤t (ideal-ctxt-closed t)

counitâ»Â¹ : ğ“• â‰¤â± Î± (U ğ“•)
counitâ»Â¹ .*â‰¤â±* ğ“•x = leaf _ ğ“•x , â‰¤-refl

counit-â‰ˆâ± : ğ“• â‰ˆâ± Î± (U ğ“•)
counit-â‰ˆâ± = counitâ»Â¹ , counit

unit : F â‰¤áµ– U (Î± F)
unit .*â‰¤áµ–* Fx = leaf _ Fx , â‰¤-refl

------------------------------------------------------------------------------
-- Binary meets

_âˆ§â±_ : Ideal â†’ Ideal â†’ Ideal
(ğ“• âˆ§â± ğ“–) .ICarrier x = ğ“• .ICarrier x Ã— ğ“– .ICarrier x
(ğ“• âˆ§â± ğ“–) .â‰¤-closed xâ‰¤y (ğ“•y , ğ“–y) = ğ“• .â‰¤-closed xâ‰¤y ğ“•y , ğ“– .â‰¤-closed xâ‰¤y ğ“–y
(ğ“• âˆ§â± ğ“–) .+-closed (ğ“•x , ğ“–x) (ğ“•y , ğ“–y) = (ğ“• .+-closed ğ“•x ğ“•y) , (ğ“– .+-closed ğ“–x ğ“–y)

projâ‚â± : (ğ“• âˆ§â± ğ“–) â‰¤â± ğ“•
projâ‚â± .*â‰¤â±* = projâ‚

projâ‚‚â± : (ğ“• âˆ§â± ğ“–) â‰¤â± ğ“–
projâ‚‚â± .*â‰¤â±* = projâ‚‚

âŸ¨_,_âŸ©â± : ğ“• â‰¤â± ğ“– â†’ ğ“• â‰¤â± ğ“— â†’ ğ“• â‰¤â± (ğ“– âˆ§â± ğ“—)
âŸ¨ ğ“—â‰¤ğ“• , ğ“—â‰¤ğ“– âŸ©â± .*â‰¤â±* = < ğ“—â‰¤ğ“• .*â‰¤â±* , ğ“—â‰¤ğ“– .*â‰¤â±* >

âˆ§â±-isMeetSemilattice : IsMeetSemilattice _â‰ˆâ±_ _â‰¤â±_ _âˆ§â±_
âˆ§â±-isMeetSemilattice = record
  { isPartialOrder = â‰¤â±-isPartialOrder
  ; infimum        = Î» ğ“• ğ“– â†’ (projâ‚â± ,  projâ‚‚â± , Î» ğ“— â†’ âŸ¨_,_âŸ©â±)
  }

-- FIXME: under what conditions does Î± preserve meets?

------------------------------------------------------------------------------
-- Binary joins

_âˆ¨â±_ : Ideal â†’ Ideal â†’ Ideal
ğ“• âˆ¨â± ğ“– = Î± (U ğ“• âˆ¨áµ– U ğ“–)

injâ‚â± : ğ“• â‰¤â± (ğ“• âˆ¨â± ğ“–)
injâ‚â± = â‰¤â±-trans counitâ»Â¹ (Î±-mono injâ‚áµ–)

injâ‚‚â± : ğ“– â‰¤â± (ğ“• âˆ¨â± ğ“–)
injâ‚‚â± = â‰¤â±-trans counitâ»Â¹ (Î±-mono injâ‚‚áµ–)

[_,_]â± : ğ“• â‰¤â± ğ“— â†’ ğ“– â‰¤â± ğ“— â†’ (ğ“• âˆ¨â± ğ“–) â‰¤â± ğ“—
[_,_]â± {ğ“•} {ğ“—} {ğ“–} ğ“•â‰¤ğ“— ğ“–â‰¤ğ“— .*â‰¤â±* (t , xâ‰¤t) =
  ğ“— .â‰¤-closed (â‰¤-trans xâ‰¤t (ctxt-map-sum _ t)) (ideal-ctxt-closed (ctxt-map [ U-mono ğ“•â‰¤ğ“— , U-mono ğ“–â‰¤ğ“— ]áµ– t))

âˆ¨â±-isJoinSemilattice : IsJoinSemilattice _â‰ˆâ±_ _â‰¤â±_ _âˆ¨â±_
âˆ¨â±-isJoinSemilattice = record
  { isPartialOrder = â‰¤â±-isPartialOrder
  ; supremum       = Î» ğ“• ğ“– â†’ (injâ‚â± , injâ‚‚â± , Î» ğ“— â†’ [_,_]â±)
  }


hulp : (c : ctxt (Î·áµ– (x + y))) â†’ Î£[ d âˆˆ ctxt (U (Î± (Î·áµ– x) âˆ¨â± Î± (Î·áµ– y))) ] (sum c â‰¤ sum d)
hulp {x}{y} (leaf z (lift zâ‰¤x+y)) =
  (node (leaf x (injâ‚â± .*â‰¤â±* ((leaf x (lift â‰¤-refl)) , â‰¤-refl)))
        (leaf y (injâ‚‚â± .*â‰¤â±* ((leaf y (lift â‰¤-refl)) , â‰¤-refl)))) ,
  zâ‰¤x+y
hulp (node câ‚ câ‚‚) =
  let (dâ‚ , câ‚â‰¤dâ‚) , (dâ‚‚ , câ‚‚â‰¤dâ‚‚) = hulp câ‚ , hulp câ‚‚
  in node dâ‚ dâ‚‚ , +-mono câ‚â‰¤dâ‚ câ‚‚â‰¤dâ‚‚

Î·-preserve-+ : Î± (Î·áµ– (x + y)) â‰¤â± Î± (Î·áµ– x) âˆ¨â± Î± (Î·áµ– y)
Î·-preserve-+ {x}{y} .*â‰¤â±* {z} (c , zâ‰¤c) =
  let d , câ‰¤d = hulp c in down-closed (â‰¤-trans zâ‰¤c câ‰¤d) (ideal-ctxt-closed d)
  where open Ideal (Î± (Î·áµ– x) âˆ¨â± Î± (Î·áµ– y)) renaming (â‰¤-closed to down-closed)


------------------------------------------------------------------------------
module DayEntropic {_âˆ™_ Îµ}
    (isPomonoid : IsPomonoid _â‰ˆ_ _â‰¤_ _âˆ™_ Îµ)
    (+-entropy : Entropy _â‰¤_ _+_ _âˆ™_)
    (+-tidy    : Îµ + Îµ â‰¤ Îµ)
    where

  _â—â±_ : Ideal â†’ Ideal â†’ Ideal
  (ğ“• â—â± ğ“–) .ICarrier x =
    âˆƒ[ y ] âˆƒ[ z ] (x â‰¤ (y âˆ™ z) Ã— ğ“• .ICarrier y Ã— ğ“– .ICarrier z)
  (ğ“• â—â± ğ“–) .â‰¤-closed xâ‰¤w (y , z , wâ‰¤yz , ğ“•y , ğ“–z) =
    (-, -, â‰¤-trans xâ‰¤w wâ‰¤yz , ğ“•y , ğ“–z)
  (ğ“• â—â± ğ“–) .+-closed (yâ‚ , zâ‚ , xâ‚â‰¤yâ‚zâ‚ , Ï•â‚ , Ïˆâ‚) (yâ‚‚ , zâ‚‚ , xâ‚‚â‰¤yâ‚‚zâ‚‚ , Ï•â‚‚ , Ïˆâ‚‚) =
    yâ‚ + yâ‚‚ , zâ‚ + zâ‚‚ ,
    â‰¤-trans (+-mono xâ‚â‰¤yâ‚zâ‚ xâ‚‚â‰¤yâ‚‚zâ‚‚) (+-entropy _ _ _ _) ,
    ğ“• .+-closed Ï•â‚ Ï•â‚‚ ,
    ğ“– .+-closed Ïˆâ‚ Ïˆâ‚‚

  Î¹â± : Ideal
  Î¹â± .ICarrier x = Lift c (x â‰¤ Îµ)
  Î¹â± .â‰¤-closed xâ‰¤y (lift yâ‰¤Îµ) = lift (â‰¤-trans xâ‰¤y yâ‰¤Îµ)
  Î¹â± .+-closed (lift xâ‰¤Îµ) (lift yâ‰¤Îµ) = lift (â‰¤-trans (+-mono xâ‰¤Îµ yâ‰¤Îµ) +-tidy)

  open P.LiftIsPomonoid isPomonoid

  â—â±-mono : Monotonicâ‚‚ _â‰¤â±_ _â‰¤â±_ _â‰¤â±_ _â—â±_
  â—â±-mono ğ“•â‚â‰¤ğ“–â‚ ğ“•â‚‚â‰¤ğ“–â‚‚ .*â‰¤â±* = âˆ™áµ–-mono (U-mono ğ“•â‚â‰¤ğ“–â‚) (U-mono ğ“•â‚‚â‰¤ğ“–â‚‚) .*â‰¤áµ–*

  â—â±-assoc : Associative _â‰ˆâ±_ _â—â±_
  â—â±-assoc ğ“• ğ“– ğ“— .projâ‚ .*â‰¤â±* = âˆ™áµ–-assoc (U ğ“•) (U ğ“–) (U ğ“—) .projâ‚ .*â‰¤áµ–*
  â—â±-assoc ğ“• ğ“– ğ“— .projâ‚‚ .*â‰¤â±* = âˆ™áµ–-assoc (U ğ“•) (U ğ“–) (U ğ“—) .projâ‚‚ .*â‰¤áµ–*

  â—â±-identityË¡ : LeftIdentity _â‰ˆâ±_ Î¹â± _â—â±_
  â—â±-identityË¡ ğ“• .projâ‚ .*â‰¤â±* = âˆ™áµ–-identityË¡ (U ğ“•) .projâ‚ .*â‰¤áµ–*
  â—â±-identityË¡ ğ“• .projâ‚‚ .*â‰¤â±* = âˆ™áµ–-identityË¡ (U ğ“•) .projâ‚‚ .*â‰¤áµ–*

  â—â±-identityÊ³ : RightIdentity _â‰ˆâ±_ Î¹â± _â—â±_
  â—â±-identityÊ³ ğ“• .projâ‚ .*â‰¤â±* = âˆ™áµ–-identityÊ³ (U ğ“•) .projâ‚ .*â‰¤áµ–*
  â—â±-identityÊ³ ğ“• .projâ‚‚ .*â‰¤â±* = âˆ™áµ–-identityÊ³ (U ğ“•) .projâ‚‚ .*â‰¤áµ–*

  â—â±-identity : Identity _â‰ˆâ±_ Î¹â± _â—â±_
  â—â±-identity = (â—â±-identityË¡ , â—â±-identityÊ³)

  â—â±-isPomonoid : IsPomonoid _â‰ˆâ±_ _â‰¤â±_ _â—â±_ Î¹â±
  â—â±-isPomonoid = record
    { isPosemigroup = record
      { isPomagma = record
        { isPartialOrder = â‰¤â±-isPartialOrder
        ; mono = â—â±-mono
        }
      ; assoc = â—â±-assoc
      }
    ; identity = â—â±-identity
    }

  U-monoidal : U (ğ“• â—â± ğ“–) â‰ˆáµ– (U ğ“• âˆ™áµ– U ğ“–)
  U-monoidal .projâ‚ .*â‰¤áµ–* ğ“•x = ğ“•x
  U-monoidal .projâ‚‚ .*â‰¤áµ–* ğ“•x = ğ“•x

  U-monoidal-Î¹ : U Î¹â± â‰ˆáµ– Îµáµ–
  U-monoidal-Î¹ .projâ‚ .*â‰¤áµ–* xâ‰¤Îµ = xâ‰¤Îµ
  U-monoidal-Î¹ .projâ‚‚ .*â‰¤áµ–* xâ‰¤Îµ = xâ‰¤Îµ

  Î·â±-preserve-â— : Î·â± (x âˆ™ y) â‰¤â± Î·â± x â—â± Î·â± y
  Î·â±-preserve-â— {x}{y} .*â‰¤â±* {z} (c , zâ‰¤c) =
    down-closed
      (â‰¤-trans zâ‰¤c (ctxt-map-sum _ c))
      (ideal-ctxt-closed {Î± (Î·áµ– x) â—â± Î± (Î·áµ– y)}
         (ctxt-map (â‰¤áµ–-trans Î·-preserve-âˆ™ (â‰¤áµ–-trans (âˆ™áµ–-mono unit unit) (U-monoidal .projâ‚‚))) c))
    where open Ideal (Î± (Î·áµ– x) â—â± Î± (Î·áµ– y)) renaming (â‰¤-closed to down-closed)

{-
  -- FIXME: this doesn't work
  module _ (idem : âˆ€ {x} â†’ x + x â‰¤ x) where

    open IsPomonoid isPomonoid using (mono)

    -- FIXME: this is the same combination function as below
    _âˆ™á¶œ'_ : ctxt F â†’ ctxt G â†’ ctxt (F âˆ™áµ– G)
    leaf x Fx  âˆ™á¶œ' leaf y Gy  = leaf (x âˆ™ y) (x , y , â‰¤-refl , Fx , Gy)
    leaf x Fx  âˆ™á¶œ' node dâ‚ dâ‚‚ = node (leaf x Fx âˆ™á¶œ' dâ‚) (leaf x Fx âˆ™á¶œ' dâ‚‚)
    node câ‚ câ‚‚ âˆ™á¶œ' d          = node (câ‚ âˆ™á¶œ' d) (câ‚‚ âˆ™á¶œ' d)

    âˆ™á¶œ-sum : (c : ctxt F)(d : ctxt G) â†’ sum (c âˆ™á¶œ' d) â‰¤ sum c âˆ™ sum d
    âˆ™á¶œ-sum (leaf x Fx)  (leaf y Gy)  = â‰¤-refl
    âˆ™á¶œ-sum (leaf x Fx)  (node dâ‚ dâ‚‚) =
       â‰¤-trans (+-mono (âˆ™á¶œ-sum (leaf x Fx) dâ‚) (âˆ™á¶œ-sum (leaf x Fx) dâ‚‚))
      (â‰¤-trans (+-entropy _ _ _ _)
               (mono idem â‰¤-refl))
    âˆ™á¶œ-sum (node câ‚ câ‚‚) d =
      â‰¤-trans (+-mono (âˆ™á¶œ-sum câ‚ d) (âˆ™á¶œ-sum câ‚‚ d))
      (â‰¤-trans (+-entropy _ _ _ _)
      (mono â‰¤-refl idem))

    Î·â±-preserve-â—â»Â¹ : Î± (Î·áµ– x) â—â± Î± (Î·áµ– y) â‰¤â± Î± (Î·áµ– (x âˆ™ y))
    Î·â±-preserve-â—â»Â¹ {x}{y} .*â‰¤â±* {z} (zâ‚ , zâ‚‚ , zâ‰¤zâ‚zâ‚‚ , (câ‚ , zâ‚â‰¤c) , (câ‚‚ , zâ‚‚â‰¤c)) =
      ctxt-map Î·-preserve-âˆ™â»Â¹ (câ‚ âˆ™á¶œ' câ‚‚) ,
      â‰¤-trans zâ‰¤zâ‚zâ‚‚ {!!}
-}

module DayDistributive
    {_âˆ™_} {Îµ}
    (isCommutativePomonoid : IsCommutativePomonoid _â‰ˆ_ _â‰¤_ _âˆ™_ Îµ)
    (distrib : _DistributesOver_ _â‰¤_ _âˆ™_ _+_)
  where

  open IsCommutativePomonoid isCommutativePomonoid
  open P.LiftIsCommutativePomonoid isCommutativePomonoid

  distribË¡ = distrib .projâ‚
  distribÊ³ = distrib .projâ‚‚

  _âˆ™â±_ : Ideal â†’ Ideal â†’ Ideal
  ğ“• âˆ™â± ğ“– = Î± (U ğ“• âˆ™áµ– U ğ“–)

  Îµâ± : Ideal
  Îµâ± = Î± Îµáµ–

  _âˆ™á¶œ_ : ctxt F â†’ ctxt G â†’ ctxt (F âˆ™áµ– G)
  leaf x Fx  âˆ™á¶œ leaf y Gy  = leaf (x âˆ™ y) (x , y , â‰¤-refl , Fx , Gy)
  leaf x Fx  âˆ™á¶œ node dâ‚ dâ‚‚ = node (leaf x Fx âˆ™á¶œ dâ‚) (leaf x Fx âˆ™á¶œ dâ‚‚)
  node câ‚ câ‚‚ âˆ™á¶œ d          = node (câ‚ âˆ™á¶œ d) (câ‚‚ âˆ™á¶œ d)

  âˆ™á¶œ-sum : (c : ctxt F)(d : ctxt G) â†’ sum c âˆ™ sum d â‰¤ sum (c âˆ™á¶œ d)
  âˆ™á¶œ-sum (leaf x Fx)  (leaf y Gy)  = â‰¤-refl
  âˆ™á¶œ-sum (leaf x Fx)  (node dâ‚ dâ‚‚) = â‰¤-trans (distribË¡ _ _ _) (+-mono (âˆ™á¶œ-sum (leaf x Fx) dâ‚) (âˆ™á¶œ-sum (leaf x Fx) dâ‚‚))
  âˆ™á¶œ-sum (node câ‚ câ‚‚) d            = â‰¤-trans (distribÊ³ _ _ _) (+-mono (âˆ™á¶œ-sum câ‚ d) (âˆ™á¶œ-sum câ‚‚ d))

  Î±-helper : (c : ctxt (U (Î± F) âˆ™áµ– U (Î± G))) â†’ x â‰¤ sum c â†’ Î£[ d âˆˆ ctxt (F âˆ™áµ– G) ] (x â‰¤ sum d)
  Î±-helper (leaf y (yâ‚ , yâ‚‚ , yâ‰¤yâ‚yâ‚‚ , (c , yâ‚â‰¤c) , (d , yâ‚‚â‰¤d))) xâ‰¤y =
    (c âˆ™á¶œ d) , â‰¤-trans xâ‰¤y (â‰¤-trans yâ‰¤yâ‚yâ‚‚ (â‰¤-trans (mono yâ‚â‰¤c yâ‚‚â‰¤d) (âˆ™á¶œ-sum c d)))
  Î±-helper (node c d) xâ‰¤cd =
    let (c' , câ‰¤c') , (d' , dâ‰¤d') = Î±-helper c â‰¤-refl , Î±-helper d â‰¤-refl
    in (node c' d') , (â‰¤-trans xâ‰¤cd (+-mono câ‰¤c' dâ‰¤d'))

  Î±-monoidal : (Î± F âˆ™â± Î± G) â‰ˆâ± Î± (F âˆ™áµ– G)
  Î±-monoidal .projâ‚ .*â‰¤â±* (c , xâ‰¤c)  = Î±-helper c xâ‰¤c
  Î±-monoidal .projâ‚‚ = Î±-mono (âˆ™áµ–-mono unit unit)

  âˆ™â±-mono : Monotonicâ‚‚ _â‰¤â±_ _â‰¤â±_ _â‰¤â±_ _âˆ™â±_
  âˆ™â±-mono ğ“•â‚â‰¤ğ“•â‚‚ ğ“–â‚â‰¤ğ“–â‚‚ = Î±-mono (âˆ™áµ–-mono (U-mono ğ“•â‚â‰¤ğ“•â‚‚) (U-mono ğ“–â‚â‰¤ğ“–â‚‚))

  Î·â±-preserve-âˆ™ : Î·â± (x âˆ™ y) â‰¤â± Î·â± x âˆ™â± Î·â± y
  Î·â±-preserve-âˆ™ = Î±-mono (â‰¤áµ–-trans Î·-preserve-âˆ™ (âˆ™áµ–-mono unit unit))

  Î·â±-preserve-âˆ™â»Â¹ : Î·â± x âˆ™â± Î·â± y â‰¤â± Î·â± (x âˆ™ y)
  Î·â±-preserve-âˆ™â»Â¹ = â‰¤â±-trans (Î±-monoidal .projâ‚) (Î±-mono Î·-preserve-âˆ™â»Â¹)

  âˆ™â±-assoc : Associative _â‰ˆâ±_ _âˆ™â±_
  âˆ™â±-assoc ğ“• ğ“– ğ“— =
    begin
      (ğ“• âˆ™â± ğ“–) âˆ™â± ğ“—
    â‰¡âŸ¨âŸ©
      Î± (U (Î± (U ğ“• âˆ™áµ– U ğ“–)) âˆ™áµ– U ğ“—)
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-congË¡ (U-cong counit-â‰ˆâ±)) âŸ©
      Î± (U (Î± (U ğ“• âˆ™áµ– U ğ“–)) âˆ™áµ– U (Î± (U ğ“—)))
    â‰ˆâŸ¨ Î±-monoidal âŸ©
      Î± ((U ğ“• âˆ™áµ– U ğ“–) âˆ™áµ– U ğ“—)
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-assoc (U ğ“•) (U ğ“–) (U ğ“—)) âŸ©
      Î± (U ğ“• âˆ™áµ– (U ğ“– âˆ™áµ– U ğ“—))
    â‰ˆâŸ¨ Î±-monoidal âŸ¨
      Î± (U (Î± (U ğ“•)) âˆ™áµ– U (Î± (U ğ“– âˆ™áµ– U ğ“—)))
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-congÊ³ (U-cong counit-â‰ˆâ±)) âŸ¨
      Î± (U ğ“• âˆ™áµ– U (Î± (U ğ“– âˆ™áµ– U ğ“—)))
    â‰¡âŸ¨âŸ©
      ğ“• âˆ™â± (ğ“– âˆ™â± ğ“—)
    âˆ
    where open SetoidReasoning â‰ˆâ±-setoid

  âˆ™â±-identityË¡ : LeftIdentity _â‰ˆâ±_ Îµâ± _âˆ™â±_
  âˆ™â±-identityË¡ ğ“• =
    begin
      Îµâ± âˆ™â± ğ“•
    â‰¡âŸ¨âŸ©
      Î± (U (Î± Îµáµ–) âˆ™áµ– U ğ“•)
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-congË¡ (U-cong counit-â‰ˆâ±)) âŸ©
      Î± (U (Î± Îµáµ–) âˆ™áµ– U (Î± (U ğ“•)))
    â‰ˆâŸ¨ Î±-monoidal âŸ©
      Î± (Îµáµ– âˆ™áµ– U ğ“•)
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-identityË¡ (U ğ“•)) âŸ©
      Î± (U ğ“•)
    â‰ˆâŸ¨ counit-â‰ˆâ± âŸ¨
      ğ“•
    âˆ
    where open SetoidReasoning â‰ˆâ±-setoid

  âˆ™â±-identityÊ³ : RightIdentity _â‰ˆâ±_ Îµâ± _âˆ™â±_
  âˆ™â±-identityÊ³ ğ“• =
    begin
      ğ“• âˆ™â± Îµâ±
    â‰¡âŸ¨âŸ©
      Î± (U ğ“• âˆ™áµ– U (Î± Îµáµ–))
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-congÊ³ (U-cong counit-â‰ˆâ±)) âŸ©
      Î± (U (Î± (U ğ“•)) âˆ™áµ– U (Î± Îµáµ–))
    â‰ˆâŸ¨ Î±-monoidal âŸ©
      Î± (U ğ“• âˆ™áµ– Îµáµ–)
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-identityÊ³ (U ğ“•)) âŸ©
      Î± (U ğ“•)
    â‰ˆâŸ¨ counit-â‰ˆâ± âŸ¨
      ğ“•
    âˆ
    where open SetoidReasoning â‰ˆâ±-setoid

  âˆ™â±-comm : Commutative _â‰ˆâ±_ _âˆ™â±_
  âˆ™â±-comm ğ“• ğ“– = Î±-cong (âˆ™áµ–-comm (U ğ“•) (U ğ“–))

  âˆ™â±-isCommutativePomonoid : IsCommutativePomonoid _â‰ˆâ±_ _â‰¤â±_ _âˆ™â±_ Îµâ±
  âˆ™â±-isCommutativePomonoid = record
    { isPomonoid = record
      { isPosemigroup = record
        { isPomagma = record
          { isPartialOrder = â‰¤â±-isPartialOrder
          ; mono = âˆ™â±-mono
          }
        ; assoc = âˆ™â±-assoc
        }
      ; identity = âˆ™â±-identityË¡ , âˆ™â±-identityÊ³
      }
      ; comm = âˆ™â±-comm
    }

  commutativePomonoid : CommutativePomonoid (suc (c âŠ” â„“â‚‚)) (c âŠ” â„“â‚‚) (c âŠ” â„“â‚‚)
  commutativePomonoid =
    record { isCommutativePomonoid = âˆ™â±-isCommutativePomonoid }

  ------------------------------------------------------------------------------
  -- Residuals

  _âŠ¸â±_ : Ideal â†’ Ideal â†’ Ideal
  (ğ“• âŠ¸â± ğ“–) .ICarrier x = âˆ€ {y} â†’ ğ“• .ICarrier y â†’ ğ“– .ICarrier (x âˆ™ y)
  (ğ“• âŠ¸â± ğ“–) .â‰¤-closed xâ‰¤z f ğ“•y = ğ“– .â‰¤-closed (monoË¡ xâ‰¤z) (f ğ“•y)
  (ğ“• âŠ¸â± ğ“–) .+-closed ğ“•âŠ¸ğ“–x ğ“•âŠ¸ğ“–y {z} ğ“•z =
    ğ“– .â‰¤-closed (distribÊ³ _ _ _) (ğ“– .+-closed (ğ“•âŠ¸ğ“–x ğ“•z) (ğ“•âŠ¸ğ“–y ğ“•z))

  UâŠ¸â± : U (ğ“• âŠ¸â± ğ“–) â‰¤áµ– (U ğ“• â‡¨áµ– U ğ“–)
  UâŠ¸â± .*â‰¤áµ–* f = f

  UâŠ¸â±â»Â¹ : (U ğ“• â‡¨áµ– U ğ“–) â‰¤áµ– U (ğ“• âŠ¸â± ğ“–)
  UâŠ¸â±â»Â¹ .*â‰¤áµ–* f = f

  UâŠ¸â±-â‰ˆáµ– : U (ğ“• âŠ¸â± ğ“–) â‰ˆáµ– (U ğ“• â‡¨áµ– U ğ“–)
  UâŠ¸â±-â‰ˆáµ– = (UâŠ¸â± , UâŠ¸â±â»Â¹)

  âŠ¸â±-residual-to : (ğ“• âˆ™â± ğ“–) â‰¤â± ğ“— â†’ ğ“– â‰¤â± (ğ“• âŠ¸â± ğ“—)
  âŠ¸â±-residual-to ğ“•ğ“–â‰¤ğ“— =
    â‰¤â±-trans counitâ»Â¹
   (â‰¤â±-trans (Î±-mono (â‡¨áµ–-residual-to (â‰¤áµ–-trans unit (U-mono ğ“•ğ“–â‰¤ğ“—))))
   (â‰¤â±-trans (Î±-mono UâŠ¸â±â»Â¹)
             counit))

  âŠ¸â±-residual-from : ğ“– â‰¤â± (ğ“• âŠ¸â± ğ“—) â†’ (ğ“• âˆ™â± ğ“–) â‰¤â± ğ“—
  âŠ¸â±-residual-from {ğ“–} {ğ“•} {ğ“—} ğ“–â‰¤ğ“•â‡¨ğ“— =
    begin
      ğ“• âˆ™â± ğ“–
    â‰¡âŸ¨âŸ©
      Î± (U ğ“• âˆ™áµ– U ğ“–)
    â‰¤âŸ¨ Î±-mono (â‡¨áµ–-residual-from (â‰¤áµ–-trans (U-mono ğ“–â‰¤ğ“•â‡¨ğ“—) UâŠ¸â±)) âŸ©
      Î± (U ğ“—)
    â‰ˆâŸ¨ counit-â‰ˆâ± âŸ¨
      ğ“—
    âˆ
    where open PosetReasoning â‰¤â±-poset

  âŠ¸â±-residual : RightResidual _â‰¤â±_ _âˆ™â±_ _âŠ¸â±_
  âŠ¸â±-residual .Function.Equivalence.to        = âŠ¸â±-residual-to
  âŠ¸â±-residual .Function.Equivalence.from      = âŠ¸â±-residual-from
  âŠ¸â±-residual .Function.Equivalence.to-cong   = Î» { PropEq.refl â†’ PropEq.refl }
  âŠ¸â±-residual .Function.Equivalence.from-cong = Î» { PropEq.refl â†’ PropEq.refl }

  âŠ¸â±-âˆ™â±-isResiduatedCommutativePomonoid : IsResiduatedCommutativePomonoid _â‰ˆâ±_ _â‰¤â±_ _âˆ™â±_ _âŠ¸â±_ Îµâ±
  âŠ¸â±-âˆ™â±-isResiduatedCommutativePomonoid = record
    { isCommutativePomonoid = âˆ™â±-isCommutativePomonoid
    ; residuated = commâˆ§residualâ‡’residuated â‰¤â±-isPreorder âˆ™â±-comm âŠ¸â±-residual
    }

module DayDuoidal
    {_âˆ™_} {_â—_} {Îµ} {Î¹}
    (isDuoidal : IsDuoidal _â‰ˆ_ _â‰¤_ _âˆ™_ _â—_ Îµ Î¹)
    (comm : Commutative _â‰ˆ_ _âˆ™_)
    (distrib : _DistributesOver_ _â‰¤_ _âˆ™_ _+_)
    (+-entropy : Entropy _â‰¤_ _+_ _â—_)
    (+-tidy : Î¹ + Î¹ â‰¤ Î¹)
  where

  open IsDuoidal isDuoidal

  âˆ™-isCommutativePomonoid : IsCommutativePomonoid _â‰ˆ_ _â‰¤_ _âˆ™_ Îµ
  âˆ™-isCommutativePomonoid = record
    { isPomonoid = âˆ™-isPomonoid
    ; comm       = comm
    }

  open DayDistributive âˆ™-isCommutativePomonoid distrib
  open DayEntropic â—-isPomonoid +-entropy +-tidy
  open P.LiftIsDuoidal isDuoidal

  âˆ™â±-â—â±-entropy : Entropy _â‰¤â±_ _âˆ™â±_ _â—â±_
  âˆ™â±-â—â±-entropy ğ“•â‚ ğ“–â‚ ğ“•â‚‚ ğ“–â‚‚ =
    begin
      (ğ“•â‚ â—â± ğ“–â‚) âˆ™â± (ğ“•â‚‚ â—â± ğ“–â‚‚)
    â‰¡âŸ¨âŸ©
      Î± (U (ğ“•â‚ â—â± ğ“–â‚) âˆ™áµ– U (ğ“•â‚‚ â—â± ğ“–â‚‚))
    â‰ˆâŸ¨ Î±-cong (âˆ™áµ–-cong U-monoidal U-monoidal) âŸ©
      Î± ((U ğ“•â‚ â—áµ– U ğ“–â‚) âˆ™áµ– (U ğ“•â‚‚ â—áµ– U ğ“–â‚‚))
    â‰¤âŸ¨ Î±-mono (âˆ™áµ–-â—áµ–-entropy (U ğ“•â‚) (U ğ“–â‚) (U ğ“•â‚‚) (U ğ“–â‚‚)) âŸ©
      Î± ((U ğ“•â‚ âˆ™áµ– U ğ“•â‚‚) â—áµ– (U ğ“–â‚ âˆ™áµ– U ğ“–â‚‚))
    â‰¤âŸ¨ Î±-mono (â—áµ–-mono unit unit) âŸ©
      Î± (U (Î± (U ğ“•â‚ âˆ™áµ– U ğ“•â‚‚)) â—áµ– U (Î± (U ğ“–â‚ âˆ™áµ– U ğ“–â‚‚)))
    â‰ˆâŸ¨ Î±-cong U-monoidal âŸ¨
      Î± (U (Î± (U ğ“•â‚ âˆ™áµ– U ğ“•â‚‚) â—â± Î± (U ğ“–â‚ âˆ™áµ– U ğ“–â‚‚)))
    â‰ˆâŸ¨ counit-â‰ˆâ± âŸ¨
      Î± (U ğ“•â‚ âˆ™áµ– U ğ“•â‚‚) â—â± Î± (U ğ“–â‚ âˆ™áµ– U ğ“–â‚‚)
    â‰¡âŸ¨âŸ©
      (ğ“•â‚ âˆ™â± ğ“•â‚‚) â—â± (ğ“–â‚ âˆ™â± ğ“–â‚‚)
    âˆ
    where open PosetReasoning â‰¤â±-poset

  tidy : (c : ctxt Î¹áµ–) â†’ sum c â‰¤ Î¹
  tidy (leaf x (lift xâ‰¤Î¹)) = xâ‰¤Î¹
  tidy (node c d) = â‰¤-trans (+-mono (tidy c) (tidy d)) +-tidy

  Îµâ±â‰¤Î¹â± : Îµâ± â‰¤â± Î¹â±
  Îµâ±â‰¤Î¹â± .*â‰¤â±* (t , xâ‰¤t) = lift (â‰¤-trans xâ‰¤t (â‰¤-trans (ctxt-map-sum Îµáµ–â‰¤Î¹áµ– t) (tidy (ctxt-map Îµáµ–â‰¤Î¹áµ– t))))

  âˆ™â±-â—â±-isDuoidal : IsDuoidal _â‰ˆâ±_ _â‰¤â±_ _âˆ™â±_ _â—â±_ Îµâ± Î¹â±
  âˆ™â±-â—â±-isDuoidal = record
    { âˆ™-isPomonoid = IsCommutativePomonoid.isPomonoid âˆ™â±-isCommutativePomonoid
    ; â—-isPomonoid = â—â±-isPomonoid
    ; âˆ™-â—-entropy = âˆ™â±-â—â±-entropy
    ; âˆ™-idem-Î¹ = â‰¤â±-trans (Î±-mono (âˆ™áµ–-mono (U-monoidal-Î¹ .projâ‚) (U-monoidal-Î¹ .projâ‚)))
                (â‰¤â±-trans (Î±-mono âˆ™áµ–-idem-Î¹áµ–)
                (â‰¤â±-trans (Î±-mono (U-monoidal-Î¹ .projâ‚‚))
                          counit))
    ; â—-idem-Îµ = â‰¤â±-trans (Î±-mono â—áµ–-idem-Îµáµ–)
                (â‰¤â±-trans (Î±-mono (â—áµ–-mono unit unit))
                (â‰¤â±-trans (Î±-mono (U-monoidal .projâ‚‚))
                counit))
    ; Îµâ‰²Î¹ = Îµâ±â‰¤Î¹â±
    }
