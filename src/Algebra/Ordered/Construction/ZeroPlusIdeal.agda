{-# OPTIONS --postfix-projections --safe --without-K --no-exact-split #-}

open import Level
open import Algebra
open import Algebra.Definitions
open import Algebra.Ordered
open import Algebra.Ordered.Definitions
open import Algebra.Ordered.Consequences
import Algebra.Ordered.Construction.LowerSet
open import Algebra.Ordered.Structures.Residuated
open import Algebra.Ordered.Structures.Duoidal
open import Function using (const; flip)
open import Data.Product as Product using (_Ã—_; _,_; -,_; Î£-syntax; âˆƒ; âˆƒ-syntax)
open import Data.Sum as Sum using (_âŠ_)
open import Data.Unit as Unit using ()
open import Relation.Binary
open import Relation.Binary.Construct.Core.Symmetric as SymCore using (SymCore)
open import Relation.Binary.Lattice
open import Relation.Binary.PropositionalEquality as PropEq using (_â‰¡_)
import Relation.Binary.Construct.Flip.EqAndOrd as Flip
open import Relation.Unary using (Pred; _âŠ†_)
import Relation.Binary.Reasoning.PartialOrder as PosetReasoning
import Relation.Binary.Reasoning.Setoid as SetoidReasoning

module Algebra.Ordered.Construction.ZeroPlusIdeal {c â„“â‚ â„“â‚‚}
         (pomagma : Pomagma c â„“â‚ â„“â‚‚)
         (ğŸ˜ : pomagma .Pomagma.Carrier)
     where

private
  module C = Pomagma pomagma

open C
  using
    ( Carrier
    )
  renaming
    ( _âˆ™_ to _âˆ¨á¶œ_
    ; _â‰¤_ to _â‰¤á¶œ_
    ; _â‰ˆ_ to _â‰ˆá¶œ_
    )

private
  module L = Algebra.Ordered.Construction.LowerSet C.poset

open L using (LowerSet)

private
  variable
    w x y z : Carrier
    â„“x â„“y â„“z : Level
    X : Pred Carrier â„“x
    Y : Pred Carrier â„“y
    Z : Pred Carrier â„“z
    F Fâ‚ Fâ‚‚ : LowerSet
    G Gâ‚ Gâ‚‚ : LowerSet
    H Hâ‚ Hâ‚‚ : LowerSet

record Ideal : Set (suc (c âŠ” â„“â‚‚)) where
  no-eta-equality
  field
    ICarrier : Carrier â†’ Set (c âŠ” â„“â‚‚)
    â‰¤-closed : x â‰¤á¶œ y â†’ ICarrier y â†’ ICarrier x
    0-closed : ICarrier ğŸ˜
    âˆ¨-closed : ICarrier x â†’ ICarrier y â†’ ICarrier (x âˆ¨á¶œ y)
open Ideal public

private
  variable
    I Iâ‚ Iâ‚‚ : Ideal
    J Jâ‚ Jâ‚‚ : Ideal
    K Kâ‚ Kâ‚‚ : Ideal

infix 4 _â‰¤_

record _â‰¤_ (I J : Ideal) : Set (c âŠ” â„“â‚‚) where
  no-eta-equality
  field
    *â‰¤* : I .ICarrier âŠ† J .ICarrier
open _â‰¤_ public

infix 4 _â‰ˆ_

_â‰ˆ_ : Ideal â†’ Ideal â†’ Set (c âŠ” â„“â‚‚)
_â‰ˆ_ = SymCore _â‰¤_

â‰¤-refl : I â‰¤ I
â‰¤-refl .*â‰¤* Ix = Ix

â‰¤-trans : I â‰¤ J â†’ J â‰¤ K â†’ I â‰¤ K
â‰¤-trans Iâ‰¤J Jâ‰¤K .*â‰¤* z = Jâ‰¤K .*â‰¤* (Iâ‰¤J .*â‰¤* z)

-- FIXME: get rid of the propositional equality here
â‰¤-isPartialOrder : IsPartialOrder _â‰ˆ_ _â‰¤_
â‰¤-isPartialOrder = SymCore.isPreorderâ‡’isPartialOrder _â‰¤_ â‰¡-â‰¤-isPreorder
  where
    â‰¡-â‰¤-isPreorder : IsPreorder _â‰¡_ _â‰¤_
    â‰¡-â‰¤-isPreorder = record
      { isEquivalence = PropEq.isEquivalence
      ; reflexive = Î» { PropEq.refl â†’ â‰¤-refl }
      ; trans = â‰¤-trans
      }

open IsPartialOrder â‰¤-isPartialOrder
  using
    ( module Eq
    )
  renaming
    ( â‰¤-respË¡-â‰ˆ  to â‰¤-respË¡-â‰ˆ
    ; reflexive  to â‰¤-reflexive
    ; isPreorder to â‰¤-isPreorder
    )
  public

â‰¤-poset : Poset _ _ _
â‰¤-poset = record
  { isPartialOrder = â‰¤-isPartialOrder
  }

â‰ˆ-setoid : Setoid _ _
â‰ˆ-setoid = record
  { isEquivalence = Poset.isEquivalence â‰¤-poset
  }

------------------------------------------------------------------------------
-- From ideals to lower sets
U : Ideal â†’ LowerSet
U I .L.ICarrier = I .ICarrier
U I .L.â‰¤-closed = I .â‰¤-closed

U-mono : I â‰¤ J â†’ U I L.â‰¤ U J
U-mono Iâ‰¤J .L.*â‰¤* = Iâ‰¤J .*â‰¤*

U-cong : I â‰ˆ J â†’ U I L.â‰ˆ U J
U-cong (Jâ‰¤I , Iâ‰¤J) = U-mono Jâ‰¤I , U-mono Iâ‰¤J

------------------------------------------------------------------------------
-- Turn a lower set into an ideal by closing under âˆ¨

data `â‹ (F : LowerSet) : Set (c âŠ” â„“â‚‚) where
  leaf : (x : Carrier) â†’ F .L.ICarrier x â†’ `â‹ F
  `ğŸ˜ : `â‹ F
  node : `â‹ F â†’ `â‹ F â†’ `â‹ F

`â‹-eval : `â‹ F â†’ Carrier
`â‹-eval (leaf x _) = x
`â‹-eval `ğŸ˜        = ğŸ˜
`â‹-eval (node c d) = `â‹-eval c âˆ¨á¶œ `â‹-eval d

`â‹-map : F L.â‰¤ G â†’ `â‹ F â†’ `â‹ G
`â‹-map Fâ‰¤G (leaf x Fx) = leaf x (Fâ‰¤G .L.*â‰¤* Fx)
`â‹-map Fâ‰¤G `ğŸ˜         = `ğŸ˜
`â‹-map Fâ‰¤G (node c d)  = node (`â‹-map Fâ‰¤G c) (`â‹-map Fâ‰¤G d)

`â‹-map-eval : (Fâ‰¤G : F L.â‰¤ G) (c : `â‹ F) â†’ `â‹-eval c â‰¤á¶œ `â‹-eval (`â‹-map Fâ‰¤G c)
`â‹-map-eval Fâ‰¤G (leaf x Fx) = C.refl
`â‹-map-eval Fâ‰¤G `ğŸ˜         = C.refl
`â‹-map-eval Fâ‰¤G (node c d) = C.mono (`â‹-map-eval Fâ‰¤G c) (`â‹-map-eval Fâ‰¤G d)

Î± : LowerSet â†’ Ideal
Î± F .ICarrier x = Î£[ t âˆˆ `â‹ F ] (x â‰¤á¶œ `â‹-eval t)
Î± F .â‰¤-closed xâ‰¤y (t , yâ‰¤t) = t , C.trans xâ‰¤y yâ‰¤t
Î± F .0-closed = `ğŸ˜ , C.refl
Î± F .âˆ¨-closed (s , xâ‰¤s) (t , yâ‰¤t) = node s t , C.mono xâ‰¤s yâ‰¤t

Î±-mono : F L.â‰¤ G â†’ Î± F â‰¤ Î± G
Î±-mono Fâ‰¤G .*â‰¤* (t , xâ‰¤t) = `â‹-map Fâ‰¤G t , C.trans xâ‰¤t (`â‹-map-eval Fâ‰¤G t)

Î±-cong : âˆ€ {F G} â†’ F L.â‰ˆ G â†’ Î± F â‰ˆ Î± G
Î±-cong (Gâ‰¤F , Fâ‰¤G) = (Î±-mono Gâ‰¤F , Î±-mono Fâ‰¤G)

------------------------------------------------------------------------------
Î· : Carrier â†’ Ideal
Î· x = Î± (L.Î· x)

Î·-mono : x â‰¤á¶œ y â†’ Î· x â‰¤ Î· y
Î·-mono xâ‰¤y = Î±-mono (L.Î·-mono xâ‰¤y)

------------------------------------------------------------------------------
-- U and Î± form a Galois connection

`â‹-closed : (t : `â‹ (U I)) â†’ I .ICarrier (`â‹-eval t)
`â‹-closed {I} (leaf x Ï•) = Ï•
`â‹-closed {I} `ğŸ˜         = I .0-closed
`â‹-closed {I} (node c d) = I .âˆ¨-closed (`â‹-closed c) (`â‹-closed d)

counit : Î± (U I) â‰¤ I
counit {I} .*â‰¤* (t , xâ‰¤t) = I .â‰¤-closed xâ‰¤t (`â‹-closed t)

counitâ»Â¹ : I â‰¤ Î± (U I)
counitâ»Â¹ .*â‰¤* Ix = leaf _ Ix , C.refl

counit-â‰ˆ : I â‰ˆ Î± (U I)
counit-â‰ˆ = counitâ»Â¹ , counit

unit : F L.â‰¤ U (Î± F)
unit .L.*â‰¤* Fx = leaf _ Fx , C.refl

------------------------------------------------------------------------------
-- Binary meets

_âˆ§_ : Ideal â†’ Ideal â†’ Ideal
(I âˆ§ J) .ICarrier x = I .ICarrier x Ã— J .ICarrier x
(I âˆ§ J) .â‰¤-closed xâ‰¤y (Iy , Jy) = I .â‰¤-closed xâ‰¤y Iy , J .â‰¤-closed xâ‰¤y Jy
(I âˆ§ J) .0-closed = I .0-closed , J .0-closed
(I âˆ§ J) .âˆ¨-closed (Ix , Jx) (Iy , Jy) = (I .âˆ¨-closed Ix Iy) , (J .âˆ¨-closed Jx Jy)

projâ‚ : (I âˆ§ J) â‰¤ I
projâ‚ .*â‰¤* = Product.projâ‚

projâ‚‚ : (I âˆ§ J) â‰¤ J
projâ‚‚ .*â‰¤* = Product.projâ‚‚

âŸ¨_,_âŸ© : I â‰¤ J â†’ I â‰¤ K â†’ I â‰¤ (J âˆ§ K)
âŸ¨ Kâ‰¤I , Kâ‰¤J âŸ© .*â‰¤* = Product.< Kâ‰¤I .*â‰¤* , Kâ‰¤J .*â‰¤* >

âˆ§-isMeetSemilattice : IsMeetSemilattice _â‰ˆ_ _â‰¤_ _âˆ§_
âˆ§-isMeetSemilattice = record
  { isPartialOrder = â‰¤-isPartialOrder
  ; infimum        = Î» I J â†’ (projâ‚ ,  projâ‚‚ , Î» K â†’ âŸ¨_,_âŸ©)
  }

-- FIXME: under what conditions does Î± preserve meets?

âŠ¤ : Ideal
âŠ¤ .ICarrier x = Lift _ Unit.âŠ¤
âŠ¤ .â‰¤-closed xâ‰¤y (lift Unit.tt) = lift Unit.tt
âŠ¤ .0-closed = lift Unit.tt
âŠ¤ .âˆ¨-closed _ _ = lift Unit.tt

âŠ¤-maximum : Maximum _â‰¤_ âŠ¤
âŠ¤-maximum x .*â‰¤* _ = lift Unit.tt

âˆ§-âŠ¤-isBoundedMeetSemilattice : IsBoundedMeetSemilattice _â‰ˆ_ _â‰¤_ _âˆ§_ âŠ¤
âˆ§-âŠ¤-isBoundedMeetSemilattice = record
  { isMeetSemilattice = âˆ§-isMeetSemilattice
  ; maximum = âŠ¤-maximum
  }

------------------------------------------------------------------------------
-- Binary joins

_âˆ¨_ : Ideal â†’ Ideal â†’ Ideal
I âˆ¨ J = Î± (U I L.âˆ¨ U J)

âŠ¥ : Ideal
âŠ¥ = Î± L.âŠ¥

injâ‚ : I â‰¤ (I âˆ¨ J)
injâ‚ = â‰¤-trans counitâ»Â¹ (Î±-mono L.injâ‚)

injâ‚‚ : J â‰¤ (I âˆ¨ J)
injâ‚‚ = â‰¤-trans counitâ»Â¹ (Î±-mono L.injâ‚‚)

[_,_] : I â‰¤ K â†’ J â‰¤ K â†’ (I âˆ¨ J) â‰¤ K
[_,_] {I} {K} {J} Iâ‰¤K Jâ‰¤K .*â‰¤* (t , xâ‰¤t) =
  K .â‰¤-closed (C.trans xâ‰¤t (`â‹-map-eval _ t)) (`â‹-closed (`â‹-map L.[ U-mono Iâ‰¤K , U-mono Jâ‰¤K ] t))

âˆ¨-isJoinSemilattice : IsJoinSemilattice _â‰ˆ_ _â‰¤_ _âˆ¨_
âˆ¨-isJoinSemilattice = record
  { isPartialOrder = â‰¤-isPartialOrder
  ; supremum       = Î» I J â†’ (injâ‚ , injâ‚‚ , Î» K â†’ [_,_])
  }

âŠ¥-minimum : Minimum _â‰¤_ âŠ¥
âŠ¥-minimum x = â‰¤-trans (Î±-mono (L.âŠ¥-minimum (U x))) counit

âˆ¨-âŠ¥-isBoundedJoinSemilattice : IsBoundedJoinSemilattice _â‰ˆ_ _â‰¤_ _âˆ¨_ âŠ¥
âˆ¨-âŠ¥-isBoundedJoinSemilattice = record
  { isJoinSemilattice = âˆ¨-isJoinSemilattice
  ; minimum = âŠ¥-minimum
  }

private
  helper : (c : `â‹ (L.Î· (x âˆ¨á¶œ y))) â†’ Î£[ d âˆˆ `â‹ (U (Î± (L.Î· x) âˆ¨ Î± (L.Î· y))) ] (`â‹-eval c â‰¤á¶œ `â‹-eval d)
  helper {x}{y} (leaf z (lift zâ‰¤xâˆ¨y)) =
    (node (leaf x (injâ‚ .*â‰¤* ((leaf x (lift C.refl)) , C.refl)))
          (leaf y (injâ‚‚ .*â‰¤* ((leaf y (lift C.refl)) , C.refl)))) ,
    zâ‰¤xâˆ¨y
  helper `ğŸ˜ = `ğŸ˜ , C.refl
  helper (node câ‚ câ‚‚) =
    let (dâ‚ , câ‚â‰¤dâ‚) , (dâ‚‚ , câ‚‚â‰¤dâ‚‚) = helper câ‚ , helper câ‚‚
    in node dâ‚ dâ‚‚ , C.mono câ‚â‰¤dâ‚ câ‚‚â‰¤dâ‚‚

Î·-preserve-âˆ¨ : Î± (L.Î· (x âˆ¨á¶œ y)) â‰¤ Î± (L.Î· x) âˆ¨ Î± (L.Î· y)
Î·-preserve-âˆ¨ {x}{y} .*â‰¤* {z} (c , zâ‰¤c) =
  let d , câ‰¤d = helper c in
    Ideal.â‰¤-closed (Î± (L.Î· x) âˆ¨ Î± (L.Î· y))
      (C.trans zâ‰¤c câ‰¤d) (`â‹-closed d)

private
  preserve-ğŸ˜-helper : (c : `â‹ (L.Î· ğŸ˜)) â†’ Î£[ d âˆˆ `â‹ L.âŠ¥ ] (`â‹-eval c â‰¤á¶œ `â‹-eval d)
  preserve-ğŸ˜-helper (leaf x (lift xâ‰¤ğŸ˜)) = `ğŸ˜ , xâ‰¤ğŸ˜
  preserve-ğŸ˜-helper `ğŸ˜ = `ğŸ˜ , C.refl
  preserve-ğŸ˜-helper (node câ‚ câ‚‚) =
    let (dâ‚ , câ‚â‰¤dâ‚) , (dâ‚‚ , câ‚‚â‰¤dâ‚‚) = preserve-ğŸ˜-helper câ‚ , preserve-ğŸ˜-helper câ‚‚
    in node dâ‚ dâ‚‚ , C.mono câ‚â‰¤dâ‚ câ‚‚â‰¤dâ‚‚

Î·-preserve-ğŸ˜ : Î± (L.Î· ğŸ˜) â‰¤ âŠ¥
Î·-preserve-ğŸ˜ .*â‰¤* (c , xâ‰¤c) =
  let d , câ‰¤d = preserve-ğŸ˜-helper c in d , C.trans xâ‰¤c câ‰¤d

------------------------------------------------------------------------------
module DayEntropic
    {_âˆ™á¶œ_}
    {Îµá¶œ}
    (isPomonoid : IsPomonoid _â‰ˆá¶œ_ _â‰¤á¶œ_ _âˆ™á¶œ_ Îµá¶œ)
    (âˆ¨-entropy : Entropy _â‰¤á¶œ_ _âˆ¨á¶œ_ _âˆ™á¶œ_)
    (âˆ¨-tidy    : Îµá¶œ âˆ¨á¶œ Îµá¶œ â‰¤á¶œ Îµá¶œ)
    (ğŸ˜-expand : ğŸ˜ â‰¤á¶œ ğŸ˜ âˆ™á¶œ ğŸ˜)
    (ğŸ˜â‰¤Îµá¶œ     : ğŸ˜ â‰¤á¶œ Îµá¶œ)
    where

  private
    module LMon = L.LiftIsPomonoid isPomonoid

  _â—_ : Ideal â†’ Ideal â†’ Ideal
  (I â— J) .ICarrier x =
    âˆƒ[ y ] âˆƒ[ z ] (x â‰¤á¶œ (y âˆ™á¶œ z) Ã— I .ICarrier y Ã— J .ICarrier z)
  (I â— J) .â‰¤-closed xâ‰¤w (y , z , wâ‰¤yz , Iy , Jz) =
    (-, -, C.trans xâ‰¤w wâ‰¤yz , Iy , Jz)
  (I â— J) .0-closed = ğŸ˜ , ğŸ˜ , ğŸ˜-expand , I .0-closed , J .0-closed
  (I â— J) .âˆ¨-closed (yâ‚ , zâ‚ , xâ‚â‰¤yâ‚zâ‚ , Ï•â‚ , Ïˆâ‚) (yâ‚‚ , zâ‚‚ , xâ‚‚â‰¤yâ‚‚zâ‚‚ , Ï•â‚‚ , Ïˆâ‚‚) =
    yâ‚ âˆ¨á¶œ yâ‚‚ , zâ‚ âˆ¨á¶œ zâ‚‚ ,
    C.trans (C.mono xâ‚â‰¤yâ‚zâ‚ xâ‚‚â‰¤yâ‚‚zâ‚‚) (âˆ¨-entropy _ _ _ _) ,
    I .âˆ¨-closed Ï•â‚ Ï•â‚‚ ,
    J .âˆ¨-closed Ïˆâ‚ Ïˆâ‚‚

  Î¹ : Ideal
  Î¹ .ICarrier x = Lift c (x â‰¤á¶œ Îµá¶œ)
  Î¹ .â‰¤-closed xâ‰¤y (lift yâ‰¤Îµ) = lift (C.trans xâ‰¤y yâ‰¤Îµ)
  Î¹ .0-closed = lift ğŸ˜â‰¤Îµá¶œ
  Î¹ .âˆ¨-closed (lift xâ‰¤Îµ) (lift yâ‰¤Îµ) = lift (C.trans (C.mono xâ‰¤Îµ yâ‰¤Îµ) âˆ¨-tidy)

  â—-mono : Monotonicâ‚‚ _â‰¤_ _â‰¤_ _â‰¤_ _â—_
  â—-mono Iâ‚â‰¤Jâ‚ Iâ‚‚â‰¤Jâ‚‚ .*â‰¤* = LMon.âˆ™-mono (U-mono Iâ‚â‰¤Jâ‚) (U-mono Iâ‚‚â‰¤Jâ‚‚) .L.*â‰¤*

  â—-assoc : Associative _â‰ˆ_ _â—_
  â—-assoc I J K .Product.projâ‚ .*â‰¤* = LMon.âˆ™-assoc (U I) (U J) (U K) .Product.projâ‚ .L.*â‰¤*
  â—-assoc I J K .Product.projâ‚‚ .*â‰¤* = LMon.âˆ™-assoc (U I) (U J) (U K) .Product.projâ‚‚ .L.*â‰¤*

  â—-identityË¡ : LeftIdentity _â‰ˆ_ Î¹ _â—_
  â—-identityË¡ I .Product.projâ‚ .*â‰¤* = LMon.âˆ™-identityË¡ (U I) .Product.projâ‚ .L.*â‰¤*
  â—-identityË¡ I .Product.projâ‚‚ .*â‰¤* = LMon.âˆ™-identityË¡ (U I) .Product.projâ‚‚ .L.*â‰¤*

  â—-identityÊ³ : RightIdentity _â‰ˆ_ Î¹ _â—_
  â—-identityÊ³ I .Product.projâ‚ .*â‰¤* = LMon.âˆ™-identityÊ³ (U I) .Product.projâ‚ .L.*â‰¤*
  â—-identityÊ³ I .Product.projâ‚‚ .*â‰¤* = LMon.âˆ™-identityÊ³ (U I) .Product.projâ‚‚ .L.*â‰¤*

  â—-identity : Identity _â‰ˆ_ Î¹ _â—_
  â—-identity = (â—-identityË¡ , â—-identityÊ³)

  â—-isPomonoid : IsPomonoid _â‰ˆ_ _â‰¤_ _â—_ Î¹
  â—-isPomonoid = record
    { isPosemigroup = record
      { isPomagma = record
        { isPartialOrder = â‰¤-isPartialOrder
        ; mono = â—-mono
        }
      ; assoc = â—-assoc
      }
    ; identity = â—-identity
    }

  U-monoidal : U (I â— J) L.â‰ˆ (U I LMon.âˆ™ U J)
  U-monoidal .Product.projâ‚ .L.*â‰¤* Ix = Ix
  U-monoidal .Product.projâ‚‚ .L.*â‰¤* Ix = Ix

  U-monoidal-Î¹ : U Î¹ L.â‰ˆ LMon.Îµ
  U-monoidal-Î¹ .Product.projâ‚ .L.*â‰¤* xâ‰¤Îµ = xâ‰¤Îµ
  U-monoidal-Î¹ .Product.projâ‚‚ .L.*â‰¤* xâ‰¤Îµ = xâ‰¤Îµ

  Î·-preserve-â— : Î· (x âˆ™á¶œ y) â‰¤ Î· x â— Î· y
  Î·-preserve-â— {x} {y} .*â‰¤* {z} (c , zâ‰¤c) =
    Ideal.â‰¤-closed
      (Î± (L.Î· x) â— Î± (L.Î· y))
        (C.trans zâ‰¤c (`â‹-map-eval _ c))
          (`â‹-closed {Î± (L.Î· x) â— Î± (L.Î· y)} 
            (`â‹-map 
              (L.â‰¤-trans LMon.Î·-preserve-âˆ™ 
                (L.â‰¤-trans (LMon.âˆ™-mono unit unit) (U-monoidal .Product.projâ‚‚))) c))

{-
  -- FIXME: this doesn't work
  module _ (idem : âˆ€ {x} â†’ x âˆ¨á¶œ x â‰¤á¶œ x) where

    open IsPomonoid isPomonoid using (mono)

    -- FIXME: this is the same combination function as below
    _âˆ™á¶œ'_ : `â‹ F â†’ `â‹ G â†’ `â‹ (F LMon.âˆ™ G)
    leaf x Fx  âˆ™á¶œ' leaf y Gy  = leaf (x âˆ™á¶œ y) (x , y , C.refl , Fx , Gy)
    leaf x Fx  âˆ™á¶œ' node dâ‚ dâ‚‚ = node (leaf x Fx âˆ™á¶œ' dâ‚) (leaf x Fx âˆ™á¶œ' dâ‚‚)
    node câ‚ câ‚‚ âˆ™á¶œ' d          = node (câ‚ âˆ™á¶œ' d) (câ‚‚ âˆ™á¶œ' d)

    âˆ™áµ—-`â‹-eval : (c : `â‹ F)(d : `â‹ G) â†’ `â‹-eval (c âˆ™á¶œ' d) â‰¤á¶œ `â‹-eval c âˆ™á¶œ `â‹-eval d
    âˆ™áµ—-`â‹-eval (leaf x Fx)  (leaf y Gy)  = C.refl
    âˆ™áµ—-`â‹-eval (leaf x Fx)  (node dâ‚ dâ‚‚) =
       C.trans (C.mono (âˆ™áµ—-`â‹-eval (leaf x Fx) dâ‚) (âˆ™áµ—-`â‹-eval (leaf x Fx) dâ‚‚))
      (C.trans (âˆ¨-entropy _ _ _ _)
               (mono idem C.refl))
    âˆ™áµ—-`â‹-eval (node câ‚ câ‚‚) d =
      C.trans (C.mono (âˆ™áµ—-`â‹-eval câ‚ d) (âˆ™áµ—-`â‹-eval câ‚‚ d))
      (C.trans (âˆ¨-entropy _ _ _ _)
      (mono C.refl idem))

    Î·-preserve-â—â»Â¹ : Î± (Î· x) â— Î± (Î· y) â‰¤ Î± (Î· (x âˆ™á¶œ y))
    Î·-preserve-â—â»Â¹ {x}{y} .*â‰¤* {z} (zâ‚ , zâ‚‚ , zâ‰¤zâ‚zâ‚‚ , (câ‚ , zâ‚â‰¤c) , (câ‚‚ , zâ‚‚â‰¤c)) =
      `â‹-map Î·-preserve-âˆ™â»Â¹ (câ‚ âˆ™á¶œ' câ‚‚) ,
      C.trans zâ‰¤zâ‚zâ‚‚ {!!}
-}

module DayDistributive
    {_âˆ™á¶œ_}
    {Îµá¶œ}
    (isCommutativePomonoid : IsCommutativePomonoid _â‰ˆá¶œ_ _â‰¤á¶œ_ _âˆ™á¶œ_ Îµá¶œ)
    (distrib : _DistributesOver_ _â‰¤á¶œ_ _âˆ™á¶œ_ _âˆ¨á¶œ_)
    (ğŸ˜-distrib : âˆ€ {x} â†’ ğŸ˜ âˆ™á¶œ x â‰¤á¶œ ğŸ˜)
  where

  private
    module Mon = IsCommutativePomonoid isCommutativePomonoid
    module LMon = L.LiftIsCommutativePomonoid isCommutativePomonoid

  distribË¡ = distrib .Product.projâ‚
  distribÊ³ = distrib .Product.projâ‚‚

  _âˆ™_ : Ideal â†’ Ideal â†’ Ideal
  I âˆ™ J = Î± (U I LMon.âˆ™ U J)

  Îµ : Ideal
  Îµ = Î± LMon.Îµ

  _âˆ™áµ—_ : `â‹ F â†’ `â‹ G â†’ `â‹ (F LMon.âˆ™ G)
  leaf x Fx  âˆ™áµ— leaf y Gy  = leaf (x âˆ™á¶œ y) (x , y , C.refl , Fx , Gy)
  leaf x Fx  âˆ™áµ— node dâ‚ dâ‚‚ = node (leaf x Fx âˆ™áµ— dâ‚) (leaf x Fx âˆ™áµ— dâ‚‚)
  leaf x Fx  âˆ™áµ— `ğŸ˜        = `ğŸ˜
  `ğŸ˜        âˆ™áµ— leaf y Gy  = `ğŸ˜
  `ğŸ˜        âˆ™áµ— `ğŸ˜         = `ğŸ˜
  `ğŸ˜        âˆ™áµ— node dâ‚ dâ‚‚ = `ğŸ˜
  node câ‚ câ‚‚ âˆ™áµ— d          = node (câ‚ âˆ™áµ— d) (câ‚‚ âˆ™áµ— d)

  âˆ™áµ—-`â‹-eval : (c : `â‹ F)(d : `â‹ G) â†’ `â‹-eval c âˆ™á¶œ `â‹-eval d â‰¤á¶œ `â‹-eval (c âˆ™áµ— d)
  âˆ™áµ—-`â‹-eval (leaf x Fx)  (leaf y Gy)  = C.refl
  âˆ™áµ—-`â‹-eval (leaf x Fx)  (node dâ‚ dâ‚‚) = C.trans (distribË¡ _ _ _) (C.mono (âˆ™áµ—-`â‹-eval (leaf x Fx) dâ‚) (âˆ™áµ—-`â‹-eval (leaf x Fx) dâ‚‚))
  âˆ™áµ—-`â‹-eval (leaf x Fx)  `ğŸ˜           = C.trans (C.reflexive (Mon.comm _ _)) ğŸ˜-distrib
  âˆ™áµ—-`â‹-eval `ğŸ˜           (leaf x xâ‚) = ğŸ˜-distrib
  âˆ™áµ—-`â‹-eval `ğŸ˜           `ğŸ˜          = ğŸ˜-distrib
  âˆ™áµ—-`â‹-eval `ğŸ˜           (node d dâ‚) = ğŸ˜-distrib
  âˆ™áµ—-`â‹-eval (node câ‚ câ‚‚) d            = C.trans (distribÊ³ _ _ _) (C.mono (âˆ™áµ—-`â‹-eval câ‚ d) (âˆ™áµ—-`â‹-eval câ‚‚ d))

  Î±-helper : (c : `â‹ (U (Î± F) LMon.âˆ™ U (Î± G))) â†’ x â‰¤á¶œ `â‹-eval c â†’ Î£[ d âˆˆ `â‹ (F LMon.âˆ™ G) ] (x â‰¤á¶œ `â‹-eval d)
  Î±-helper (leaf y (yâ‚ , yâ‚‚ , yâ‰¤yâ‚yâ‚‚ , (c , yâ‚â‰¤c) , (d , yâ‚‚â‰¤d))) xâ‰¤y =
    (c âˆ™áµ— d) , C.trans xâ‰¤y (C.trans yâ‰¤yâ‚yâ‚‚ (C.trans (Mon.mono yâ‚â‰¤c yâ‚‚â‰¤d) (âˆ™áµ—-`â‹-eval c d)))
  Î±-helper `ğŸ˜ xâ‰¤y = `ğŸ˜ , xâ‰¤y
  Î±-helper (node c d) xâ‰¤cd =
    let (c' , câ‰¤c') , (d' , dâ‰¤d') = Î±-helper c C.refl , Î±-helper d C.refl
    in (node c' d') , (C.trans xâ‰¤cd (C.mono câ‰¤c' dâ‰¤d'))

  Î±-monoidal : (Î± F âˆ™ Î± G) â‰ˆ Î± (F LMon.âˆ™ G)
  Î±-monoidal .Product.projâ‚ .*â‰¤* (c , xâ‰¤c)  = Î±-helper c xâ‰¤c
  Î±-monoidal .Product.projâ‚‚ = Î±-mono (LMon.âˆ™-mono unit unit)

  âˆ™-mono : Monotonicâ‚‚ _â‰¤_ _â‰¤_ _â‰¤_ _âˆ™_
  âˆ™-mono Iâ‚â‰¤Iâ‚‚ Jâ‚â‰¤Jâ‚‚ = Î±-mono (LMon.âˆ™-mono (U-mono Iâ‚â‰¤Iâ‚‚) (U-mono Jâ‚â‰¤Jâ‚‚))

  Î·-preserve-âˆ™ : Î· (x âˆ™á¶œ y) â‰¤ Î· x âˆ™ Î· y
  Î·-preserve-âˆ™ = Î±-mono (L.â‰¤-trans LMon.Î·-preserve-âˆ™ (LMon.âˆ™-mono unit unit))

  Î·-preserve-âˆ™â»Â¹ : Î· x âˆ™ Î· y â‰¤ Î· (x âˆ™á¶œ y)
  Î·-preserve-âˆ™â»Â¹ = â‰¤-trans (Î±-monoidal .Product.projâ‚) (Î±-mono LMon.Î·-preserve-âˆ™â»Â¹)

  âˆ™-assoc : Associative _â‰ˆ_ _âˆ™_
  âˆ™-assoc I J K =
    begin
      (I âˆ™ J) âˆ™ K
    â‰¡âŸ¨âŸ©
      Î± (U (Î± (U I LMon.âˆ™ U J)) LMon.âˆ™ U K)
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-congË¡ (U-cong counit-â‰ˆ)) âŸ©
      Î± (U (Î± (U I LMon.âˆ™ U J)) LMon.âˆ™ U (Î± (U K)))
    â‰ˆâŸ¨ Î±-monoidal âŸ©
      Î± ((U I LMon.âˆ™ U J) LMon.âˆ™ U K)
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-assoc (U I) (U J) (U K)) âŸ©
      Î± (U I LMon.âˆ™ (U J LMon.âˆ™ U K))
    â‰ˆâŸ¨ Î±-monoidal âŸ¨
      Î± (U (Î± (U I)) LMon.âˆ™ U (Î± (U J LMon.âˆ™ U K)))
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-congÊ³ (U-cong counit-â‰ˆ)) âŸ¨
      Î± (U I LMon.âˆ™ U (Î± (U J LMon.âˆ™ U K)))
    â‰¡âŸ¨âŸ©
      I âˆ™ (J âˆ™ K)
    âˆ
    where open SetoidReasoning â‰ˆ-setoid

  âˆ™-identityË¡ : LeftIdentity _â‰ˆ_ Îµ _âˆ™_
  âˆ™-identityË¡ I =
    begin
      Îµ âˆ™ I
    â‰¡âŸ¨âŸ©
      Î± (U (Î± LMon.Îµ) LMon.âˆ™ U I)
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-congË¡ (U-cong counit-â‰ˆ)) âŸ©
      Î± (U (Î± LMon.Îµ) LMon.âˆ™ U (Î± (U I)))
    â‰ˆâŸ¨ Î±-monoidal âŸ©
      Î± (LMon.Îµ LMon.âˆ™ U I)
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-identityË¡ (U I)) âŸ©
      Î± (U I)
    â‰ˆâŸ¨ counit-â‰ˆ âŸ¨
      I
    âˆ
    where open SetoidReasoning â‰ˆ-setoid

  âˆ™-identityÊ³ : RightIdentity _â‰ˆ_ Îµ _âˆ™_
  âˆ™-identityÊ³ I =
    begin
      I âˆ™ Îµ
    â‰¡âŸ¨âŸ©
      Î± (U I LMon.âˆ™ U (Î± LMon.Îµ))
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-congÊ³ (U-cong counit-â‰ˆ)) âŸ©
      Î± (U (Î± (U I)) LMon.âˆ™ U (Î± LMon.Îµ))
    â‰ˆâŸ¨ Î±-monoidal âŸ©
      Î± (U I LMon.âˆ™ LMon.Îµ)
    â‰ˆâŸ¨ Î±-cong (LMon.âˆ™-identityÊ³ (U I)) âŸ©
      Î± (U I)
    â‰ˆâŸ¨ counit-â‰ˆ âŸ¨
      I
    âˆ
    where open SetoidReasoning â‰ˆ-setoid

  âˆ™-comm : Commutative _â‰ˆ_ _âˆ™_
  âˆ™-comm I J = Î±-cong (LMon.âˆ™-comm (U I) (U J))

  âˆ™-isCommutativePomonoid : IsCommutativePomonoid _â‰ˆ_ _â‰¤_ _âˆ™_ Îµ
  âˆ™-isCommutativePomonoid = record
    { isPomonoid = record
      { isPosemigroup = record
        { isPomagma = record
          { isPartialOrder = â‰¤-isPartialOrder
          ; mono = âˆ™-mono
          }
        ; assoc = âˆ™-assoc
        }
      ; identity = âˆ™-identityË¡ , âˆ™-identityÊ³
      }
      ; comm = âˆ™-comm
    }

  commutativePomonoid : CommutativePomonoid (suc (c âŠ” â„“â‚‚)) (c âŠ” â„“â‚‚) (c âŠ” â„“â‚‚)
  commutativePomonoid = record
    { isCommutativePomonoid = âˆ™-isCommutativePomonoid }

  ------------------------------------------------------------------------------
  -- Residuals

  _âŠ¸_ : Ideal â†’ Ideal â†’ Ideal
  (I âŠ¸ J) .ICarrier x = âˆ€ {y} â†’ I .ICarrier y â†’ J .ICarrier (x âˆ™á¶œ y)
  (I âŠ¸ J) .â‰¤-closed xâ‰¤z f Iy = J .â‰¤-closed (Mon.monoË¡ xâ‰¤z) (f Iy)
  (I âŠ¸ J) .0-closed Iy = J .â‰¤-closed ğŸ˜-distrib (J .0-closed)
  (I âŠ¸ J) .âˆ¨-closed IâŠ¸Jx IâŠ¸Jy {z} Iz =
    J .â‰¤-closed (distribÊ³ _ _ _) (J .âˆ¨-closed (IâŠ¸Jx Iz) (IâŠ¸Jy Iz))

  UâŠ¸ : U (I âŠ¸ J) L.â‰¤ (U I LMon.âŠ¸ U J)
  UâŠ¸ .L.*â‰¤* f = f

  UâŠ¸â»Â¹ : (U I LMon.âŠ¸ U J) L.â‰¤ U (I âŠ¸ J)
  UâŠ¸â»Â¹ .L.*â‰¤* f = f

  UâŠ¸-â‰ˆ : U (I âŠ¸ J) L.â‰ˆ (U I LMon.âŠ¸ U J)
  UâŠ¸-â‰ˆ = (UâŠ¸ , UâŠ¸â»Â¹)

  âŠ¸-residual-to : (I âˆ™ J) â‰¤ K â†’ J â‰¤ (I âŠ¸ K)
  âŠ¸-residual-to IJâ‰¤K =
    â‰¤-trans counitâ»Â¹
   (â‰¤-trans (Î±-mono (LMon.âŠ¸-residual-to (L.â‰¤-trans unit (U-mono IJâ‰¤K))))
   (â‰¤-trans (Î±-mono UâŠ¸â»Â¹)
             counit))

  âŠ¸-residual-from : J â‰¤ (I âŠ¸ K) â†’ (I âˆ™ J) â‰¤ K
  âŠ¸-residual-from {J} {I} {K} Jâ‰¤IâŠ¸K =
    begin
      I âˆ™ J
    â‰¡âŸ¨âŸ©
      Î± (U I LMon.âˆ™ U J)
    â‰¤âŸ¨ Î±-mono (LMon.âŠ¸-residual-from (L.â‰¤-trans (U-mono Jâ‰¤IâŠ¸K) UâŠ¸)) âŸ©
      Î± (U K)
    â‰ˆâŸ¨ counit-â‰ˆ âŸ¨
      K
    âˆ
    where open PosetReasoning â‰¤-poset

  âŠ¸-residual : RightResidual _â‰¤_ _âˆ™_ _âŠ¸_
  âŠ¸-residual .Function.Equivalence.to        = âŠ¸-residual-to
  âŠ¸-residual .Function.Equivalence.from      = âŠ¸-residual-from
  âŠ¸-residual .Function.Equivalence.to-cong   = Î» { PropEq.refl â†’ PropEq.refl }
  âŠ¸-residual .Function.Equivalence.from-cong = Î» { PropEq.refl â†’ PropEq.refl }

  âŠ¸-âˆ™-isResiduatedCommutativePomonoid : IsResiduatedCommutativePomonoid _â‰ˆ_ _â‰¤_ _âˆ™_ _âŠ¸_ Îµ
  âŠ¸-âˆ™-isResiduatedCommutativePomonoid = record
    { isCommutativePomonoid = âˆ™-isCommutativePomonoid
    ; residuated = commâˆ§residualâ‡’residuated â‰¤-isPreorder âˆ™-comm âŠ¸-residual
    }

module DayDuoidal
    {_âˆ™á¶œ_}
    {_â—á¶œ_}
    {Îµá¶œ}
    {Î¹á¶œ}
    (isCommutativeDuoidal : IsCommutativeDuoidal _â‰ˆá¶œ_ _â‰¤á¶œ_ _âˆ™á¶œ_ _â—á¶œ_ Îµá¶œ Î¹á¶œ)
    (distrib : _DistributesOver_ _â‰¤á¶œ_ _âˆ™á¶œ_ _âˆ¨á¶œ_)
    (ğŸ˜-distrib : âˆ€ {x} â†’ ğŸ˜ âˆ™á¶œ x â‰¤á¶œ ğŸ˜)
    (âˆ¨á¶œ-entropy : Entropy _â‰¤á¶œ_ _âˆ¨á¶œ_ _â—á¶œ_)
    (âˆ¨á¶œ-tidy : Î¹á¶œ âˆ¨á¶œ Î¹á¶œ â‰¤á¶œ Î¹á¶œ)
    (ğŸ˜-expand : ğŸ˜ â‰¤á¶œ (ğŸ˜ â—á¶œ ğŸ˜))
    (ğŸ˜â‰¤Î¹á¶œ     : ğŸ˜ â‰¤á¶œ Î¹á¶œ)
  where

  private
    module Duo = IsCommutativeDuoidal isCommutativeDuoidal
    module LDuo = L.LiftIsDuoidal Duo.isDuoidal

  open DayDistributive Duo.âˆ™-isCommutativePomonoid distrib ğŸ˜-distrib
  open DayEntropic Duo.â—-isPomonoid âˆ¨á¶œ-entropy âˆ¨á¶œ-tidy ğŸ˜-expand ğŸ˜â‰¤Î¹á¶œ

  âˆ™-â—-entropy : Entropy _â‰¤_ _âˆ™_ _â—_
  âˆ™-â—-entropy Iâ‚ Jâ‚ Iâ‚‚ Jâ‚‚ =
    begin
      (Iâ‚ â— Jâ‚) âˆ™ (Iâ‚‚ â— Jâ‚‚)
    â‰¡âŸ¨âŸ©
      Î± (U (Iâ‚ â— Jâ‚) LDuo.âˆ™ U (Iâ‚‚ â— Jâ‚‚))
    â‰ˆâŸ¨ Î±-cong (LDuo.âˆ™-cong U-monoidal U-monoidal) âŸ©
      Î± ((U Iâ‚ LDuo.â— U Jâ‚) LDuo.âˆ™ (U Iâ‚‚ LDuo.â— U Jâ‚‚))
    â‰¤âŸ¨ Î±-mono (LDuo.âˆ™-â—-entropy (U Iâ‚) (U Jâ‚) (U Iâ‚‚) (U Jâ‚‚)) âŸ©
      Î± ((U Iâ‚ LDuo.âˆ™ U Iâ‚‚) LDuo.â— (U Jâ‚ LDuo.âˆ™ U Jâ‚‚))
    â‰¤âŸ¨ Î±-mono (LDuo.â—-mono unit unit) âŸ©
      Î± (U (Î± (U Iâ‚ LDuo.âˆ™ U Iâ‚‚)) LDuo.â— U (Î± (U Jâ‚ LDuo.âˆ™ U Jâ‚‚)))
    â‰ˆâŸ¨ Î±-cong U-monoidal âŸ¨
      Î± (U (Î± (U Iâ‚ LDuo.âˆ™ U Iâ‚‚) â— Î± (U Jâ‚ LDuo.âˆ™ U Jâ‚‚)))
    â‰ˆâŸ¨ counit-â‰ˆ âŸ¨
      Î± (U Iâ‚ LDuo.âˆ™ U Iâ‚‚) â— Î± (U Jâ‚ LDuo.âˆ™ U Jâ‚‚)
    â‰¡âŸ¨âŸ©
      (Iâ‚ âˆ™ Iâ‚‚) â— (Jâ‚ âˆ™ Jâ‚‚)
    âˆ
    where open PosetReasoning â‰¤-poset

  tidy : (c : `â‹ LDuo.Î¹) â†’ `â‹-eval c â‰¤á¶œ Î¹á¶œ
  tidy (leaf x (lift xâ‰¤Î¹)) = xâ‰¤Î¹
  tidy `ğŸ˜ = ğŸ˜â‰¤Î¹á¶œ
  tidy (node c d) = C.trans (C.mono (tidy c) (tidy d)) âˆ¨á¶œ-tidy

  Îµâ‰¤Î¹ : Îµ â‰¤ Î¹
  Îµâ‰¤Î¹ .*â‰¤* (t , xâ‰¤t) = lift (C.trans xâ‰¤t (C.trans (`â‹-map-eval LDuo.Îµâ‰¤Î¹ t) (tidy (`â‹-map LDuo.Îµâ‰¤Î¹ t))))

  âˆ™-â—-isDuoidal : IsDuoidal _â‰ˆ_ _â‰¤_ _âˆ™_ _â—_ Îµ Î¹
  âˆ™-â—-isDuoidal = record
    { âˆ™-isPomonoid = IsCommutativePomonoid.isPomonoid âˆ™-isCommutativePomonoid
    ; â—-isPomonoid = â—-isPomonoid
    ; âˆ™-â—-entropy = âˆ™-â—-entropy
    ; âˆ™-idem-Î¹ = â‰¤-trans (Î±-mono (LDuo.âˆ™-mono (U-monoidal-Î¹ .Product.projâ‚) (U-monoidal-Î¹ .Product.projâ‚)))
                (â‰¤-trans (Î±-mono LDuo.âˆ™-idem-Î¹)
                (â‰¤-trans (Î±-mono (U-monoidal-Î¹ .Product.projâ‚‚))
                          counit))
    ; â—-idem-Îµ = â‰¤-trans (Î±-mono LDuo.â—-idem-Îµ)
                (â‰¤-trans (Î±-mono (LDuo.â—-mono unit unit))
                (â‰¤-trans (Î±-mono (U-monoidal .Product.projâ‚‚))
                counit))
    ; Îµâ‰²Î¹ = Îµâ‰¤Î¹
    }
  